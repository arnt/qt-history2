CONFIG *= moc thread

CONFIG(release, debug|release):DEFINES += QT_NO_DEBUG
no_keywords:DEFINES += QT_NO_KEYWORDS

# make sure that the Qt includes are before all others... this
# prevents us from picking up "stale" includes
INCLUDEPATH = $$QMAKE_INCDIR_QT $$INCLUDEPATH

!isEmpty(QT_BUILD_TREE):QMAKE_LIBDIR += $$QT_BUILD_TREE/lib
QMAKE_LIBDIR += $$QMAKE_LIBDIR_QT
for(QTLIB, $$list($$lower($$unique(QT)))) {
   qlib =
   isEqual(QTLIB, gui):qlib = QtGui
   else:isEqual(QTLIB, network):qlib = QtNetwork
   else:isEqual(QTLIB, xml):qlib = QtXml
   else:isEqual(QTLIB, opengl):qlib = QtOpenGL
   else:isEqual(QTLIB, sql):qlib = QtSql
   else:isEqual(QTLIB, core):qlib = QtCore
   else:isEqual(QTLIB, canvas):qlib = QtCanvas
   else:isEqual(QTLIB, compat):qlib = Qt3Compat
   else:message("Unknown QT: $$QTLIB"):qlib =
   !isEmpty(qlib) {
      target_qt:isEqual(TARGET, qlib) {
         warning($$TARGET cannot have a QT of $$QTLIB)
      } else {
         DEFINES *= $$upper(QT_$${QTLIB}_LIB)
	 isEqual(QTLIB, compat) {
	    QTDIR_build:warning(***USE of COMPAT inside of QTDIR!**) #just for us
	    INCLUDEPATH *= $$QMAKE_INCDIR_QT/Qt
	    DEFINES *= QT_COMPAT
	 }
	 exists($$QMAKE_INCDIR_QT/$$qlib) {
             INCLUDEPATH -= $$QMAKE_INCDIR_QT/$$qlib
             INCLUDEPATH = $$QMAKE_INCDIR_QT/$$qlib $$INCLUDEPATH
         }
	 isEqual(QTLIB, opengl):CONFIG += opengl
	 mac:!qt_no_framework:exists(/Library/Frameworks/$${qlib}.framework/$${qlib}) {
	    FRAMEWORK_INCLUDE = /Library/Frameworks/$${qlib}.framework/Headers
	    !qt_no_framework_direct_includes:exists($$FRAMEWORK_INCLUDE) {
	       INCLUDEPATH -= $$FRAMEWORK_INCLUDE
	       INCLUDEPATH = $$FRAMEWORK_INCLUDE $$INCLUDEPATH
            }
	    LIBS   += -framework $${qlib}
	 } else:CONFIG(debug, debug|release) {
	    LIBS   += -l$${qlib}_debug
	 } else { 
 	    LIBS   += -l$${qlib}
         }
      }
   }
}

plugin {
   staticlib:DEFINES += QT_STATICPLUGIN
   DEFINES += QT_PLUGIN
}
