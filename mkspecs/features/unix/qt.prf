CONFIG *= moc thread

# make sure that the Qt includes are before all others... this
# prevents us from picking up "stale" includes
INCLUDEPATH = $$QMAKE_INCDIR_QT $$INCLUDEPATH

!debug:DEFINES += QT_NO_DEBUG

!isEmpty(QMAKE_RTLDIR_QT):QMAKE_LIBDIR += $$QMAKE_RTLDIR_QT
QMAKE_LIBDIR += $$QMAKE_LIBDIR_QT
for(QTLIB, QT) {
   qlib =
   isEqual(QTLIB, gui):qlib = qt
   else:isEqual(QTLIB, network):qlib = qnetwork
   else:isEqual(QTLIB, xml):qlib = qxml
   else:isEqual(QTLIB, opengl):qlib = qopengl
   else:isEqual(QTLIB, sql):qlib = qsql
   else:isEqual(QTLIB, core):qlib = qcore
   else:isEqual(QTLIB, canvas):qlib = qcanvas
   else:isEqual(QTLIB, compat):qlib = q3compat
   else:message("Unknown QT: $$qlib):qlib =
   !isEmpty(qlib) {
      target_qt:isEqual(TARGET, qlib) {
         warning($$TARGET cannot have a QT of $$QTLIB)
      } else {
         DEFINES *= $$upper(QT_$${QTLIB}_LIB)
	 isEqual(QTLIB, compat) {
	    QTDIR_build:warning(***USE of $$upper($${QTLIB}) inside of QTDIR!**)
	    !contains(DEFINES, QT_COMPAT):DEFINES *= QT_COMPAT_WARNINGS
	 }
	 isEqual(QTLIB, opengl):CONFIG += opengl
         LIBS   += -l$${qlib}
      }
   }
}
plugin:DEFINES += QT_PLUGIN
