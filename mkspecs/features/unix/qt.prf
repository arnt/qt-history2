CONFIG *= moc
INCLUDEPATH += $$QMAKE_INCDIR_QT
!debug:DEFINES += QT_NO_DEBUG

!isEmpty(QMAKE_RTLDIR_QT):QMAKE_LIBDIR += $$QMAKE_RTLDIR_QT
QMAKE_LIBDIR += $$QMAKE_LIBDIR_QT
for(QCONF, QCONFIG) {
   qlib =
   isEqual(QCONF, gui):qlib = qt
   else:isEqual(QCONF, network):qlib = qnetwork
   else:isEqual(QCONF, xml):qlib = qxml
   else:isEqual(QCONF, opengl):qlib = qopengl
   else:isEqual(QCONF, sql):qlib = qsql
   else:isEqual(QCONF, core):qlib = qcore
   else:isEqual(QCONF, canvas):qlib = qcanvas
   else:isEqual(QCONF, compat):qlib = q3compat
   else:message("Unknown QCONFIG: $$qlib):qlib =
   !isEmpty(qlib) {
      target_qt:isEqual(TARGET, qlib) {
         warning($$TARGET cannot have a QCONFIG of $$QCONF)
      } else {
         DEFINES *= $$upper(QT_$${QCONF}_LIB)
	 isEqual(QCONF, compat):DEFINES *= QT_COMPAT
         LIBS   += -l$${qlib}
      }
   }
}
plugin:DEFINES += QT_PLUGIN

!isEmpty(QMAKE_CFLAGS_THREAD) {
   QMAKE_CFLAGS += $$QMAKE_CFLAGS_THREAD
   QMAKE_EXPORT_CFLAGS += $$QMAKE_CFLAGS_THREAD
}
!isEmpty(QMAKE_CXXFLAGS_THREAD) {
   QMAKE_CXXFLAGS += $$QMAKE_CXXFLAGS_THREAD
   QMAKE_EXPORT_CXXFLAGS += $$QMAKE_CXXFLAGS_THREAD
}
INCLUDEPATH += $$QMAKE_INCDIR_THREAD
LIBS += $$QMAKE_LIBS_THREAD
!isEmpty(QMAKE_LFLAGS_THREAD):QMAKE_LFLAGS += $$QMAKE_LFLAGS_THREAD
!isEmpty(QMAKE_CC_THREAD):QMAKE_CC = $$QMAKE_CC_THREAD
!isEmpty(QMAKE_CXX_THREAD):QMAKE_CXX = $$QMAKE_CXX_THREAD
!isEmpty(QMAKE_LINK_THREAD):QMAKE_LINK = $$QMAKE_LINK_THREAD
