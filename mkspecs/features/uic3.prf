
### remove this soon!!
win32:QMAKE_UIC = $(QTDIR)\bin\uic3.exe
else:QMAKE_UIC = $(QTDIR)/bin/uic3

win32:QMAKE_UIC3 = $(QTDIR)\bin\uic3.exe
else:QMAKE_UIC3 = $(QTDIR)/bin/uic3

uic3_decl.commands = $$QMAKE_UIC3 ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic3_decl.output = ${UI_DIR}${QMAKE_FILE_BASE}.h
uic3_decl.depends = $$QMAKE_UIC3
uic3_decl.input = UI3_FORMS             ### rename UI3_FORMS in FORMS
uic3_decl.variable_out = UICDECLS
uic3_decl.CONFIG += no_link
QMAKE_EXTRA_COMPILERS += uic3_decl

uic3_impl.commands = $$QMAKE_UIC3 -impl ${QMAKE_FILE_BASE}.h ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic3_impl.output = ${UI_DIR}${QMAKE_FILE_BASE}.cpp
uic3_impl.depends = $$QMAKE_UIC3
uic3_impl.input = UI3_FORMS             ### rename UI3_FORMS in FORMS
uic3_impl.variable_out = UICIMPLS
QMAKE_EXTRA_COMPILERS += uic3_impl

image_collection {
    uic3_emb.commands = $$QMAKE_UIC3 -embed $$TARGET ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_emb.output = qmake_image_collection.cpp
    uic3_emb.depends = $$QMAKE_UIC3
    uic3_emb.input = UI3_IMAGES         ### rename UI3_FORMS in IMAGES
    uic3_emb.CONFIG += combine
    uic3_emb.variable_out = SOURCES
    QMAKE_EXTRA_COMPILERS += uic3_emb
}
