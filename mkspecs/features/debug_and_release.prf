!contains(TEMPLATE, subdirs):!win32-msvc:!macx-xcode {
    !build_pass {
        DebugPackage.target = debug
        DebugPackage.CONFIG = debug
        ReleasePackage.target = release
        ReleasePackage.CONFIG = release
        CONFIG(debug, debug|release) {
            BUILDS += DebugPackage ReleasePackage
        } else {
            BUILDS += ReleasePackage DebugPackage
        }
    } else {
        isEmpty(QMAKE_DEBUG_RELEASE):QMAKE_DEBUG_RELEASE += OBJECTS_DIR MOC_DIR RCC_DIR UI_DIR
        debug_and_release_target:QMAKE_DEBUG_RELEASE += DESTDIR
        for(fix, QMAKE_DEBUG_RELEASE) {
            isEmpty($$fix) {
                DebugPackage:eval($$fix = debug)
                ReleasePackage:eval($$fix = release)
            } else {
                DebugPackage:eval($$fix ~= s/release/debug/gi)
                ReleasePackage:eval($$fix ~= s/debug/release/gi)
            }
        }
    }
}
