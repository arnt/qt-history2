isEmpty(QMAKE_RCC) {
    win32:QMAKE_RCC = $(QTDIR)\bin\rcc.exe
    else:QMAKE_RCC = $(QTDIR)/bin/rcc
}

isEmpty(RCC_DIR):RCC_DIR = .
isEmpty(QMAKE_RESOURCE_PREFIX):QMAKE_RESOURCE_PREFIX = /tmp/

!no_single_resource {
   rcc.CONFIG += combine
   rcc.output  = resources.cpp
} else {
   rcc.output  = $$RCC_DIR/resource_${QMAKE_FILE_BASE}.cpp
}
!contains(QMAKE_RESOURCE_FLAGS, -prefix):!isEmpty(QMAKE_RESOURCE_PREFIX):QMAKE_RESOURCE_FLAGS += -prefix $$QMAKE_RESOURCE_PREFIX
!contains(QMAKE_RESOURCE_FLAGS, -name):!isEmpty(QMAKE_RESOURCE_NAME):QMAKE_RESOURCE_FLAGS += -name $$QMAKE_RESOURCE_NAME
rcc.commands = $$QMAKE_RCC $$QMAKE_RESOURCE_FLAGS ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
rcc.depends = $$QMAKE_RCC
rcc.input = RESOURCES
rcc.variable_out = SOURCES
QMAKE_EXTRA_COMPILERS += rcc
