
isEmpty(QMAKE_UIC3) {
    win32:QMAKE_UIC3 = $(QTDIR)\bin\uic3.exe
    else:QMAKE_UIC3 = $(QTDIR)/bin/uic3
}

isEmpty(QMAKE_UIC) {  ### uncomment me!
   win32:QMAKE_UIC = $(QTDIR)\bin\uic.exe
   else:QMAKE_UIC = $(QTDIR)/bin/uic
}

isEmpty(UI_DIR):UI_DIR = .
isEmpty(UI_SOURCES_DIR):UI_SOURCES_DIR = $$UI_DIR
isEmpty(UI_HEADERS_DIR):UI_HEADERS_DIR = $$UI_DIR

uic.commands = $$QMAKE_UIC ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic.output = $$UI_HEADERS_DIR/ui_${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
uic.input = FORMS
uic.variable_out = HEADERS
uic.CONFIG += no_link
uic.name = UIC ${QMAKE_FILE_IN}
QMAKE_EXTRA_COMPILERS += uic

INCLUDEPATH += $$UI_HEADERS_DIR

uic3 {
    uic3_decl.commands = $$QMAKE_UIC3 -decl ui_${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)} ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_decl.output = $$UI_HEADERS_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
    uic3_decl.depends = $$QMAKE_UIC3 $$UI_HEADERS_DIR/ui_${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
    uic3_decl.input = FORMS
    uic3_decl.CONFIG += no_link
    uic3_decl.variable_out = HEADERS
    uic3_decl.name = UIC3 Decl ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_decl

    uic3_impl.commands = $$QMAKE_UIC3 -impl ${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)} ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_impl.output = $$UI_SOURCES_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_CPP)}
    uic3_impl.depends = $$QMAKE_UIC3 $$UI_HEADERS_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
    uic3_impl.input = FORMS
    uic3_impl.variable_out = SOURCES
    uic3_impl.name = UIC3 Impl ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_impl

    ### add a moc step
    uic3_moc.commands = $$QMAKE_MOC $(DEFINES) $(INCPATH) $$UI_HEADERS_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)} -o ${QMAKE_FILE_OUT}
    isEmpty(MOC_DIR):MOC_DIR = . #we just default to .
    uic3_moc.output = $$MOC_DIR/moc_${QMAKE_FILE_BASE}$${first(QMAKE_EXT_CPP)}
    uic3_moc.depends = $$UI_HEADERS_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
    uic3_moc.input = FORMS
    uic3_moc.variable_out = HDRMOC
    uic3_moc.name = UIC3 MOC ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_moc
}

image_collection.output = qmake_image_collection$${first(QMAKE_EXT_CPP)}
image_collection.variable_out = SOURCES
image_collection.input = IMAGES
image_collection.CONFIG += combine
image_collection.name = UIC3 Image collection in ${QMAKE_FILE_OUT}
image_collection.depends = $$QMAKE_UIC3
image_collection.commands = $$QMAKE_UIC3 -embed $$TARGET ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
QMAKE_EXTRA_COMPILERS += image_collection


