
isEmpty(QMAKE_UIC3) {
    win32:QMAKE_UIC3 = $(QTDIR)\bin\uic3.exe
    else:QMAKE_UIC3 = $(QTDIR)/bin/uic3
}

##### isEmpty(QMAKE_UIC) {  ### uncomment me!
   win32:QMAKE_UIC = $(QTDIR)\bin\uic.exe
   else:QMAKE_UIC = $(QTDIR)/bin/uic
##### }

uic.commands = $$QMAKE_UIC ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic.output = ui_${QMAKE_FILE_BASE}.h
uic.input = FORMS
uic.variable_out = HEADERS
uic.CONFIG += no_link
QMAKE_EXTRA_COMPILERS += uic

uic3 {
    uic3_decl.commands = $$QMAKE_UIC3 -decl ui_${QMAKE_FILE_BASE}.h ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_decl.output = ${QMAKE_FILE_BASE}.h
    uic3_decl.depends = ui_${QMAKE_FILE_BASE}.h
    uic3_decl.input = FORMS
    uic3_decl.CONFIG += no_link
    uic3_decl.variable_out = HEADERS
    QMAKE_EXTRA_COMPILERS += uic3_decl

    uic3_impl.commands = $$QMAKE_UIC3 -impl ${QMAKE_FILE_BASE}.h ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_impl.output = ${QMAKE_FILE_BASE}.cpp
    uic3_impl.depends = ${QMAKE_FILE_BASE}.h
    uic3_impl.input = FORMS
    uic3_impl.variable_out = SOURCES
    QMAKE_EXTRA_COMPILERS += uic3_impl

### add a moc step
    uic3_moc.commands = $$QMAKE_MOC $(DEFINES) $(INCPATH) ${QMAKE_FILE_BASE}.h -o ${QMAKE_FILE_OUT}
    uic3_moc.output = ${QMAKE_MOC_DIR}moc_${QMAKE_FILE_BASE}.cpp
    uic3_moc.depends = ${QMAKE_FILE_BASE}.h
    uic3_moc.input = FORMS
    uic3_moc.variable_out = _HDRMOC
    QMAKE_EXTRA_COMPILERS += uic3_moc
}
#### we don't have a replacement for `uic3 -embed' yet!

uic3_emb.commands = $$QMAKE_UIC3 -embed $$TARGET ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic3_emb.output = qmake_image_collection.cpp
uic3_emb.variable_out = SOURCES
uic3_emb.depends = $$QMAKE_UIC3
uic3_emb.input = IMAGES
uic3_emb.CONFIG += combine
QMAKE_EXTRA_COMPILERS += uic3_emb


