
isEmpty(QMAKE_UIC3) {
    win32:QMAKE_UIC3 = $(QTDIR)\bin\uic3.exe
    else:QMAKE_UIC3 = $(QTDIR)/bin/uic3
}

##### isEmpty(QMAKE_UIC) {  ### uncomment me!
   win32:QMAKE_UIC = $(QTDIR)\bin\uic.exe
   else:QMAKE_UIC = $(QTDIR)/bin/uic
##### }

isEmpty(UI_DIR):UI_DIR = .
isEmpty(UI_SOURCES_DIR):UI_SOURCES_DIR = $$UI_DIR
isEmpty(UI_HEADERS_DIR):UI_HEADERS_DIR = $$UI_DIR

uic.commands = $$QMAKE_UIC ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
uic.output = $$UI_HEADERS_DIR/ui_${QMAKE_FILE_BASE}.h
uic.input = FORMS
uic.variable_out = HEADERS
uic.CONFIG += no_link
uic.name = UIC ${QMAKE_FILE_IN}
QMAKE_EXTRA_COMPILERS += uic

uic3 {
    uic3_decl.commands = $$QMAKE_UIC3 -decl ui_${QMAKE_FILE_BASE}.h ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_decl.output = $$UI_HEADERS_DIR/${QMAKE_FILE_BASE}.h
    uic3_decl.depends = ui_${QMAKE_FILE_BASE}.h
    uic3_decl.input = FORMS
    uic3_decl.CONFIG += no_link
    uic3_decl.variable_out = HEADERS
    uic3_decl.name = UIC3 Decl ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_decl

    uic3_impl.commands = $$QMAKE_UIC3 -impl ${QMAKE_FILE_BASE}.h ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
    uic3_impl.output = $$UI_SOURCES_DIR/${QMAKE_FILE_BASE}.cpp
    uic3_impl.depends = ${QMAKE_FILE_BASE}.h
    uic3_impl.input = FORMS
    uic3_impl.variable_out = SOURCES
    uic3_impl.name = UIC3 Impl ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_impl

    ### add a moc step
    uic3_moc.commands = $$QMAKE_MOC $(DEFINES) $(INCPATH) ${QMAKE_FILE_BASE}.h -o ${QMAKE_FILE_OUT}
    isEmpty(MOC_DIR):MOC_DIR = . #we just default to .
    uic3_moc.output = $$MOC_DIR/moc_${QMAKE_FILE_BASE}.cpp
    uic3_moc.depends = ${QMAKE_FILE_BASE}.h
    uic3_moc.input = FORMS
    uic3_moc.variable_out = _HDRMOC
    uic3_moc.name = UIC3 MOC ${QMAKE_FILE_IN}
    QMAKE_EXTRA_COMPILERS += uic3_moc
}

image_collection.output = qmake_image_collection.cpp
image_collection.variable_out = SOURCES
image_collection.input = IMAGES
image_collection.CONFIG += combine
image_collection.name = UIC3 Image collection in ${QMAKE_FILE_OUT}
q3mimesourcefactory {
    image_collection.depends = $$QMAKE_UIC3
    image_collection.commands = $$QMAKE_UIC3 -embed $$TARGET ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
} else {
   isEmpty(QMAKE_RCC) {
      win32:QMAKE_RCC = $(QTDIR)\bin\rcc.exe
      else:QMAKE_RCC = $(QTDIR)/bin/rcc
   }
   image_collection.depends = $$QMAKE_RCC
   image_collection.commands = $$QMAKE_RCC -no-path -prefix /qt/q3mimesourcefactory ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
}
QMAKE_EXTRA_COMPILERS += image_collection


