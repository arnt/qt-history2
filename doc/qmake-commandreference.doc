/****************************************************************************
**
** Documentation for qmake commands
**
** Copyright (C) 1992-2001 Trolltech AS.  All rights reserved.
**
** This file is part of the Qt GUI Toolkit.
**
** This file may be distributed under the terms of the Q Public License
** as defined by Trolltech AS of Norway and appearing in the file
** LICENSE.QPL included in the packaging of this file.
**
** This file may be distributed and/or modified under the terms of the
** GNU General Public License version 2 as published by the Free Software
** Foundation and appearing in the file LICENSE.GPL included in the
** packaging of this file.
**
** Licensees holding valid Qt Enterprise Edition or Qt Professional Edition
** licenses may use this file in accordance with the Qt Commercial License
** Agreement provided with the Software.
**
** This file is provided AS IS with NO WARRANTY OF ANY KIND, INCLUDING THE
** WARRANTY OF DESIGN, MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.
**
** See http://www.trolltech.com/pricing.html or email sales@trolltech.com for
**   information about Qt Commercial License Agreements.
** See http://www.trolltech.com/qpl/ for QPL licensing information.
** See http://www.trolltech.com/gpl/ for GPL licensing information.
**
** Contact info@trolltech.com if any conditions of this licensing are
** not clear to you.
**
**********************************************************************/

/*! \page qmake-commandreference.html

\title qmake Command Reference

\list
\i \link #About About This Reference \endlink
\i \link #Commands Command Line Options \endlink
\i \link #SystemVariables System Variables \endlink
\i \link #Functions Functions \endlink
\i \link #Environment Environment Variables and Configuration \endlink
\endlist


\target About
\section1 About This Reference

This reference is a detailed index of all command line options,
configurations and internal variables used by the 
cross-platform makefile generation utility \e qmake.

For a higher level introduction to using \e qmake then see the
\link qmake-guide.html qmake User's Guide \endlink.

\target Commands
\section1 Command Line Options

\section2 Syntax

\code
qmake [options] files
\endcode

\section2 Options

The following options can be specified on the command line to \e qmake:

\list
\i \c -o file <br>
	\e qmake output will be directed to \e file. If this argument
	is not specified then qmake will try to guess a suitable name, if '-' is
	specified output is directed to stdout. 
\i \c -unix <br>
	\e qmake will run in unix mode.  In this mode, Unix file
	naming and path conventions will be used. This is the default mode on all Unices.
\i \c -win32 <br>
	\e qmake will run in win32 mode.  In this mode, Windows file naming and path
	conventions will be used. This is the default mode on Windows.
\i \c -d <br>
	\e qmake will output useful debugging information.
\i \c -t tmpl <br>
	\e qmake will override any set TEMPLATE variables with tmpl.
\i \c -help <br>
	\e qmake will go over these features and give some useful help.
\endlist


QMake supports two different modes of operation. The first mode (and
default) is makefile generation, in this mode qmake will take a .pro file
and turn it into a makefile. This is the mode documented in
this guide, however there is an additional mode in qmake to generate .pro
files.

To toggle between these modes you must specify in the first argument what
mode you want, if no mode is specified qmake will assume you want makefile
mode, the available mode switches are:

\list
\i \c -makefile <br>
	\e qmake output will be a makefile (\link #MakefileMode Makefile mode \endlink).
\i \c -project <br>
	\e qmake output will be a project file (\link #ProjectfileMode Project file mode \endlink).
\endlist

\target MakefileMode
\section3 Makefile Mode

In Makefile mode QMake will generate a makefile. Additionally you may
supply the following arguments in this mode:

\list
\i \c -nocache <br>
	\e qmake will ignore the .qmake.cache file.
\i \c -nodepend <br>
	\e qmake will not generate any dependency information.
\i \c -cache file <br>
	\e qmake will use \e file as the cache file, ignoring any other .qmake.cache file found
\i \c -spec spec <br>
	\e qmake will use \e spec as a path to platform-compiler information and QMAKESPEC will be ignored.
\endlist

The \c files argument can be a list of one or more project files, separated
by spaces. You may also pass qmake assignments on the commandline here and
they will be processed before all files specified, for example:

qmake -makefile -unix -o Makefile "CONFIG+=test" test.pro 

This will generate a Makefile from test.pro, with Unix pathnames, however
many of these arguments aren't necessary as they are the default.  Therefore
the line can be simplified on Unix to:

qmake "CONFIG+=test" test.pro 


\target ProjectfileMode
\section3 Projectfile Mode

In Projectfile mode QMake will generate a project file. Additionally you may
supply the following arguments in this mode:

\list
\i \c -nopwd <br>
	\e qmake will not look in your current working directory for
	source code and only use the specified \c files
\endlist

The \c files argument can be a list of files or directories, if a directory
is specified then this will be included in the DEPENDPATH variable and relevant 
code from there will be included in the generated project file, if a file is 
given it will go into the correct variable depending on extension (i.e. .ui 
files go into FORMS, .cpp files go into SOURCES, etc).

\target SystemVariables
\section1 System Variables

For a higher level introduction to \e qmake variables and how they are used, see the
\link qmake-guide.html#Variables qmake User's Guide \endlink.

\list
\i \link #FrequentlyUsedSystemVariables Frequently Used System Variables \endlink
\i \link #RarelyUsedSystemVariables Rarely Used System Variables \endlink
\endlist


\target FrequentlyUsedSystemVariables
\section2 Frequently Used System Variables

The following variables are recognized by \e qmake and are used
most frequently when creating
\link qmake-guide.html project files \endlink.

<dl>

\target CONFIG
<dt>\c CONFIG

<dd> The \c CONFIG variable specifies project configuration and
compiler options.  The values will be recognized internally by
\e qmake and have several special meaning.  They are as follows.

These \c CONFIG values control compilation flags: 

\list
\i release - Compile with optimization enabled, ignored if
    "debug" is specified
\i debug - Compile with debug options enabled
\i warn_on - The compiler should emit more warnings than normally, ignored if
     "warn_off" is specified
\i warn_off - The compiler should emit no warnings except severe warnings.
\endlist

These options define the application/library type:

\list
\i qt - The target is a Qt application/library and requires the Qt header
     files/library.  The proper include and library paths for the Qt
     library will automatically be added to the project.
\i opengl - The target requires the OpenGL (or Mesa)
    headers/libraries. The proper include and library paths for
    these libraries will automatically be added to the project.
\i thread - The target is a multi-threaded application or library.  The
    proper defines and compiler flags will automatically be added to
    the project.
\i x11 - The target is a X11 application or library.  The proper
    include paths and libraries will automatically be added to the
    project.
\i windows - The target is a Win32 window application (app only). The
    proper include paths,compiler flags and libraries will
    automatically be added to the project.
\i console - The target is a Win32 console application (app only). The
    proper include paths, compiler flags and libraries will
    automatically be added to the
    project.
\i dll - The target is a shared object/DLL.The proper
    include paths, compiler flags and libraries will automatically be
    added to the project.
\i staticlib - The target is a static library (lib only).  The proper
    compiler flags will automatically be added to the project.
\i plugin - The target is a plugin (lib only). This enables dll as well.
\endlist

The \c CONFIG variable will also be checked when resolving
\link qmake-guide.html#Scopes scope \endlink.  You may assign anything to
this variable.  

For example:

\code
CONFIG += qt console newstuff
...
newstuff {
	SOURCES += new.cpp
	HEADERS += new.h
}
\endcode


\target DEFINES
<dt>\c DEFINES

<dd>\e qmake adds the values of this variable as compiler C
preprocessor macros (-D option).  

For example:

\code 
DEFINES += USE_MY_STUFF QT_DLL
\endcode


\target DEF_FILE
<dt>\c DEF_FILE

<dd>\e {This is only used on Windows when using the 'app' template}.

Specifies a .def file to be included in the project.


\target DESTDIR
<dt>\c DESTDIR

<dd>Specifies where to put the \link #TARGET target \endlink file. 

For example:

\code 
  DESTDIR = ../../lib
\endcode

\target DLLDESTDIR
<dt>\c DLLDESTDIR

<dd>Specifies where to copy the \link #TARGET target \endlink dll.

\target HEADERS
<dt>\c HEADERS

<dd>Defines the header files for the project.  

\e qmake will generate dependency information (unless -nodepend
is specified on the \link #Commands command line \endlink) for the
specified headers.  \e qmake will also automatically detect if
\e moc is required by the classes in these headers, and add the
appropriate dependencies and files to the project for generating and
linking the moc files. 

For example:

\code
HEADERS	= myclass.h \
	  login.h \
	  mainwindow.h
\endcode

See also \link #SOURCES SOURCES \endlink.


\target INCLUDEPATH
<dt>\c INCLUDEPATH

<dd>This variable specifies the #include directories which should be
searched when compiling the project. Use ';' or a space as the
directory separator. 

For example:

\code
  INCLUDEPATH = c:\msdev\include d:\stl\include
\endcode


\target FORMS
<dt>\c FORMS

<dd>This variable specifies the .ui files (see \link
designer.html Qt Designer \endlink) to be processed through \e uic
before compiling.  All dependencies, headers and source files required
to build these .ui files will automatically be added to the project.

For example:

\code
FORMS = mydialog.ui \
	mywidget.ui \
        myconfig.ui
\endcode


\target LEXSOURCES
<dt>\c LEXSOURCES

<dd>This variable contains a list of lex source files.  All
dependencies, headers and source files will automatically be added to
the project for building these lex files.  

For example:

\code
LEXSOURCES = lexer.l
\endcode
 

\target LIBS
<dt>\c LIBS

<dd>This variable contains a list of libraries to be linked into the
project.  

For example:

\code
unix:LIBS += -lmath -L/usr/local/lib
win32:LIBS += c:\mylibs\math.lib
\endcode


\target MOC_DIR
<dt>\c MOC_DIR

<dd>This variable specifies the directory where all intermediate moc
files (see \link metaobjects.html meta object compiler\endlink)
should be placed.  

For example:

\code
unix:MOC_DIR = ../myproject/tmp
win32:MOC_DIR = c:\myproject\tmp
\endcode


\target OBJECTS_DIR
<dt>\c OBJECTS_DIR

<dd>This variable specifies the directory where all intermediate
object should be placed.  

For example:

\code
unix:OBJECTS_DIR = ../myproject/tmp
win32:OBJECTS__DIR = c:\myproject\tmp
\endcode


\target REQUIRES
<dt>\c REQUIRES

<dd>This is a special variable processed by \e qmake.  If the
contents of this variable do not appear in CONFIG by the time this
variable is assigned to then a minimal makefile will be generated that
states what dependencies (the values assigned to REQUIRES) were
missing.

This is mainly used in Qt's build system for building the examples.

<dt>\c SOURCES

<dd>This variable contains the name of all source files in the project.  

For example:

\code
SOURCES	= myclass.cpp \
	  login.cpp \
	  mainwindow.cpp

\endcode

See also \link #HEADERS HEADERS \endlink


<dt>\c SUBDIRS

<dd>This variable, when used with the 'subdir'
\link #TEMPLATE TEMPLATE \endlink contains the names of all subdirectories
to look for a project file.

For example:

\code
SUBDIRS = kernel \
	  tools
\endcode


\target TARGET
<dt>\c TARGET
<dd>This specifies the name of the target file.

For example:

\code
TEMPLATE = app
TARGET = myapp
SOURCES = main.cpp
\endcode

The above project file would produce an executable named 'myapp' on
unix and 'myapp.exe' on windows.




<dt>\c TEMPLATE

<dd>This variable contains the name of the template to use when
generating the project.  The allowed values are:

\list
\i app - Creates a makefile for building applications (the default)
\i lib - Creates a makefile for building libraries
\i subdirs - Creates a makefile for building targets in subdirectories
\i vcapp - \e {win32 only}  Creates an application project file
\i vclib - \e {win32 only}  Creates a library project file

\endlist

For example:

\code
TEMPLATE = lib
SOURCES = main.cpp
TARGET = mylib
\endcode




<dt>\c VERSION

<dd>This variable contains the version number of the library, if the
'lib' \link #TEMPLATE TEMPLATE \endlink is specified.

For example:

\code
VERSION = 1.2.3
\endcode


<dt>\c YACCSOURCES

<dd>This variable contains a list of yacc source files to be included
in the project.  All dependencies, headers and source files will
automatically be included in the project.

For example:

\code
YACCSOURCES = moc.y
\endcode

</dl>

\target RarelyUsedSystemVariables
\section2 Rarely Used System Variables

The following variables are also recognized by \e qmake but are
either internal or very rarely used.

<dl>

\target DESTDIR_TARGET
<dt>\c DESTDIR_TARGET

<dd>This variable is set internally by \e qmake which is basically the DESTDIR variable with
the TARGET variable appened at the end.  The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.


\target DSP_TEMPLATE
<dt>\c DSP_TEMPLATE

<dd>This variable is set internally by \e qmake which specifies where the dsp template file for
basing generated dsp files is stored.  The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.


\target LEXIMPLS
<dt>\c LEXIMPLS

<dd>This variable contains a list of lex implementation files.  The value
of this variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely
needs to be modified.



\target LEXOBJECTS
<dt>\c LEXOBJECTS

<dd>This variable contains the names of intermediate lex object
files.The value of this variable is typically handled by
\e qmake and rarely needs to be modified.


\target MAKEFILE
<dt>\c MAKEFILE

<dd>This variable specifies the name of the makefile which
\e qmake should use when outputting the dependency information
for building a project.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.


\target MAKEFILE_GENERATOR
<dt>\c MAKEFILE_GENERATOR

<dd>This variable contains the name of the makefile generator to use
when generating a makefile.  The value of this variable is typically
handled internally by \e qmake and rarely needs to be modified.


\target OBJECTS
<dt>\c OBJECTS

<dd>This variable is generated from the \link #SOURCES SOURCES
\endlink variable.  The extension of each source file will have been 
replaced by .o (Unix) or .obj (Win32).  The value of this variable is 
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.

\target OBJMOC
<dt>\c OBJMOC

<dd>This variable is set by \e qmake if files can be found that
contain the Q_OBJECT macro.  \c OBJMOC contains the
name of all intermediate moc object files. The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.

\target PRECOMPH
<dt>\c PRECOMPH

<dd>This variable contains a list of header files that require some
sort of pre-compilation step (such as with moc). The value of this
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.  


\target QMAKE
<dt>\c QMAKE

<dd>This variable contains the name of the \e qmake program
itself and is placed in generated makefiles.  The value of this
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.



\target QMAKESPEC_systemvariable
<dt>\c QMAKESPEC

<dd>This variable contains the name of the \e qmake
configuration to use when generating makefiles.  The value of this
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified. Use the \link #QMAKESPEC QMAKESPEC \endlink environment
variable instead.


\target QMAKE_AIX_SHLIB
<dt>\c QMAKE_AIX_SHLIB

<dd>If this variable is not empty, then this variable tells \e qmake 
to generate the
\link #TARGET TARGET \endlink as an AIX shared library.



\target QMAKE_APP_FLAG
<dt>\c QMAKE_APP_FLAG

<dd>This variable is empty unless the 'app'
\link #TEMPLATE TEMPLATE \endlink is specified.  The value of this
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.  Use the following instead:

\code
app {
	#conditional code for 'app' template here
}
\endcode




\target QMAKE_APP_OR_DLL
<dt>\c QMAKE_APP_OR_DLL

<dd>This variable is empty unless the 'app' or 'dll'
\link #TEMPLATE TEMPLATE \endlink is specified.  The value of this
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.  



\target QMAKE_AR_CMD
<dt>\c QMAKE_AR_CMD

<dd>\e {This is used on Unix platforms only}

This variable contains the command for invoking the program which
creates, modifies and extracts archives. The value of this variable is
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink 
and rarely needs to be modified.



\target QMAKE_CFLAGS_DEBUG
<dt>\c QMAKE_CFLAGS_DEBUG

<dd>This variable contains the flags for the C compiler in debug mode.The value of this variable is
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink 
and rarely needs to be modified.




\target QMAKE_CFLAGS_MT
<dt>\c QMAKE_CFLAGS_MT

<dd>This variable contains the compiler flags for creating a
multi-threaded application.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.




\target QMAKE_CFLAGS_MT_DBG
<dt>\c QMAKE_CFLAGS_MT_DBG

<dd>This variable contains the compiler flags for creating a debuggable
multi-threaded application.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.




\target QMAKE_CFLAGS_MT_DLL
<dt>\c QMAKE_CFLAGS_MT_DLL

<dd>\e {This is used on Windows only}

This variable contains the compiler flags for creating a 
multi-threaded dll.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.




\target QMAKE_CFLAGS_MT_DLLDBG
<dt>\c QMAKE_CFLAGS_MT_DLLDBG

<dd>\e {This is used on Windows only}

This variable contains the compiler flags for creating a debuggable
multi-threaded dll.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.




\target QMAKE_CFLAGS_RELEASE
<dt>\c QMAKE_CFLAGS_RELEASE

<dd>This variable contains the compiler flags for creating a non-debuggable
application.  The value of this variable is typically
handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.




\target QMAKE_CFLAGS_SHLIB
<dt>\c QMAKE_CFLAGS_SHLIB

<dd>\e {This is used on Unix platforms only}

This variable contains the compiler flags for creating a shared
library. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CFLAGS_THREAD
<dt>\c QMAKE_CFLAGS_THREAD

<dd>This variable contains the compiler flags for creating a multi-threaded
application. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CFLAGS_WARN_OFF
<dt>\c QMAKE_CFLAGS_WARN_OFF

<dd>This variable is not empty if the warn_off
\link #TEMPLATE TEMPLATE \endlink option is specified.  The value of this 
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink 
and rarely needs to be modified.



\target QMAKE_CFLAGS_WARN_ON
<dt>\c QMAKE_CFLAGS_WARN_ON

<dd>This variable is not empty if the warn_on
\link #TEMPLATE TEMPLATE \endlink option is specified.
The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CLEAN
<dt>\c QMAKE_CLEAN

<dd>This variable contains any files which are not generated files (such as moc and uic 
generated files) and object files that should be removed when using "make clean".



\target QMAKE_CXXFLAGS_DEBUG
<dt>\c QMAKE_CXXFLAGS_DEBUG

<dd>This variable contains the C++ compiler flags for creating a debuggable
application. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.



\target QMAKE_CXXFLAGS_MT
<dt>\c QMAKE_CXXFLAGS_MT

<dd>This variable contains the C++ compiler flags for creating a multi-threaded
application. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_MT_DBG
<dt>\c QMAKE_CXXFLAGS_MT_DBG

<dd>This variable contains the C++ compiler flags for creating a debuggable multi-threaded
application. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_MT_DLL
<dt>\c QMAKE_CXXFLAGS_MT_DLL

<dd>\c {This is used on Windows only}

This variable contains the C++ compiler flags for creating a multi-threaded
dll. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_MT_DLLDBG
<dt>\c QMAKE_CXXFLAGS_MT_DLLDBG

<dd>\c {This is used on Windows only}

This variable contains the C++ compiler flags for creating a multi-threaded debuggable
dll. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_RELEASE
<dt>\c QMAKE_CXXFLAGS_RELEASE

<dd>This variable contains the C++ compiler flags for creating an 
application. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_SHLIB
<dt>\c QMAKE_CXXFLAGS_SHLIB

<dd>This variable contains the C++ compiler flags for creating a
shared library.  The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_THREAD
<dt>\c QMAKE_CXXFLAGS_THREAD

<dd>This variable contains the C++ compiler flags for creating a
multi-threaded application.  The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs
to be modified.




\target QMAKE_CXXFLAGS_WARN_OFF
<dt>\c QMAKE_CXXFLAGS_WARN_OFF

<dd>This variable contains the C++ compiler flags for suppressing compiler warnings.
 The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



\target QMAKE_CXXFLAGS_WARN_ON
<dt>\c QMAKE_CXXFLAGS_WARN_ON

<dd>This variable contains C++ compiler flags for generating compiler warnings.
 The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




\target QMAKE_EXTENTION_SHLIB
<dt>\c QMAKE_EXTENTION_SHLIB

<dd>This variable contains the extention for shared libraries.  The value of this 
variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink 
and rarely needs to be modified.




\target QMAKE_FAILED_REQUIREMENTS
<dt>\c QMAKE_FAILED_REQUIREMENTS

<dd>This variable contains the list of requirements that were failed to be met when
\e qmake was ran, such as, the sql module is needed and wasn't compiled into Qt.  The 
value of this variable is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink 
and rarely needs to be modified.




\target QMAKE_FILETAGS
<dt>\c QMAKE_FILETAGS

<dd>This variable contains the file tags needed to be entered into the makefile, such as SOURCES
and HEADERS.  The value of this variable is typically handled by \e qmake or 
\link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




\target QMAKE_HPUX_SHLIB
<dt>\c QMAKE_HPUX_SHLIB

<dd>\e {This is used on Unix platforms only}

<dd>If this variable is not empty then this variable tells \e qmake to generate the
\link #TARGET TARGET \endlink as an HPUX shared library.  The value of this variable is 
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely 
needs to be modified.


<dt>\c QMAKE_HPUX_SHLIBS

<dd>\e {This is used on Unix platforms only}

<dd>If this variable is not empty then this variable tells \e qmake to generate the
\link #TARGET TARGET \endlink as an HPUX shared library.  The value of this variable is 
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely 
needs to be modified.


\target QMAKE_INCDIR
<dt>\c QMAKE_INCDIR

<dd>This variable contains the location of all known header files to be added to 
INCLUDEPATH when building an application.  The value of this variable is 
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely 
needs to be modified.



\target QMAKE_INCDIR_OPENGL
<dt>\c QMAKE_INCDIR_OPENGL

<dd>This variable contains the location of OpenGL header files to be added
to INCLUDEPATH when building an application with OpenGL support. The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



\target QMAKE_INCDIR_QT
<dt>\c QMAKE_INCDIR_QT

<dd>This variable contains the location of all known header files
paths to be added to INCLUDEPATH when building a Qt application. The value 
of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




\target QMAKE_INCDIR_THREAD
<dt>\c QMAKE_INCDIR_THREAD

<dd>This variable contains the location of all known header files
paths to be added to INCLUDEPATH when building a multi-threaded application. 
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




\target QMAKE_INCDIR_X11
<dt>\c QMAKE_INCDIR_X11

<dd>\e {This is used on Unix platforms only}

This variable contains the location of X11 header files paths to be
added to INCLUDEPATH when building a X11 application.  The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



\target QMAKE_LFLAGS_CONSOLE
<dt>\c QMAKE_LFLAGS_CONSOLE

<dd>\e {This is used on Windows only}

This variable contains link flags when building console
programs.  The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.


<dt>\c QMAKE_LFLAGS_CONSOLE_DLL

<dd>\e {This is used on Windows only}

<dd>This variable contains link flags when building console
dlls.  The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_DEBUG

<dd>This variable contains link flags when building debuggable applications.  The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_PLUGIN

<dd>This variable contains link flags when building plugins. The value
of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_QT_DLL

<dd>This variable contains link flags when building programs that
use the Qt library built as a dll. The value of this variable is
typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_RELEASE

<dd>This variable contains link flags when building applications for 
release. The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_SHAPP

<dd>This variable contains link flags when building applications which are using
the 'app' template.  The value of this variable is typically handled by
\e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_SHLIB

<dd>This variable contains link flags when building shared libraries
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_SONAME

<dd>This variable specifies the name of shared objects, such as .so or .dll.
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




<dt>\c QMAKE_LFLAGS_THREAD

<dd>This variable contains link flags when building multi-threaded projects.
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LFLAGS_WINDOWS

<dd>\e {This is used on Windows only}

This variable contains link flags when building windows projects.
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.


<dt>\c QMAKE_LFLAGS_WINDOWS_DLL

<dd>\e {This is used on Windows only}

This variable contains link flags when building windows dll projects.
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBDIR

<dd>This variable contains the location of all known library
directories.The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBDIR_FLAGS

<dd>\e {This is used on Unix platforms only}

This variable contains the location of all library
directory with -L prefixed.  The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBDIR_OPENGL

<dd>This variable contains the location of the OpenGL library
directory.The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBDIR_QT

<dd>This variable contains the location of the Qt library
directory.The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBDIR_X11

<dd>\e {This is used on Unix platforms only}

This variable contains the location of the X11 library
directory.The value of this variable is typically handled by
\e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.

<dt>\c QMAKE_LIBS

<dd>This variable contains all project libraries.  The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.


<dt>\c QMAKE_LIBS_CONSOLE

<dd>\e {This is used on Windows only}

This variable contains all project libraries that should be linked against
when building a console application.  The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




<dt>\c QMAKE_LIBS_OPENGL

<dd>This variable contains all OpenGL libraries.  The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_OPENGL_QT

<dd>This variable contains all OpenGL Qt libraries.The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_QT

<dd>This variable contains all Qt libraries.The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_QT_DLL

<dd>\e {This is used on Windows only}

This variable contains all Qt libraries when Qt is built as a dll.  The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_QT_OPENGL

<dd>This variable contains all the libraries needed to link against if 
OpenGL support is turned on. The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



<dt>\c QMAKE_LIBS_QT_THREAD

<dd>This variable contains all the libraries needed to link against if 
thread support is turned on.  The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_RT

<dd>\e {This is used with Borland compilers only}

This variable contains the runtime library needed to link against when
building an application.  The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_RTMT

<dd>\e {This is used with Borland compilers only}

This variable contains the runtime library needed to link against when
building a multi-threaded application.  The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



<dt>\c QMAKE_LIBS_THREAD

<dd>\e {This is used on Unix platforms only}

This variable contains all libraries that need to be linked against
when building a multi-threaded application.  The 
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_WINDOWS

<dd>\e {This is used on Windows only}

This variable contains all windows libraries.The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_X11

<dd>\e {This is used on Unix platforms only}

This variable contains all X11 libraries.The value of this
variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIBS_X11SM

<dd>\e {This is used on Unix platforms only}

This variable contains all X11 session management libraries.  The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LIB_FLAG
<dd>This variable is not empty if the 'lib' template is specified.  The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.



<dt>\c QMAKE_LINK_SHLIB_CMD

<dd>This variable contains the command to execute when creating a
shared library. The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_LN_SHLIB

<dd>This variable contains the command to execute when creating a link
to a shared library. The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_MAKEFILE

<dd>This variable contains the name of the makefile to create. The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_MAX_FILES

<dd>###





<dt>\c QMAKE_MOC_SRC

<dd>This variable contains the names of all moc source files to
generate and include in the project. The value of this variable is
typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c QMAKE_NOFORCE
<dd>###




<dt>\c QMAKE_QMAKE

<dd>###




<dt>\c QMAKE_QT_DLL
<dd>This variable is not empty if Qt was built as a dll.  The
value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.






<dt>\c QMAKE_RUN_CC
<dd>###




<dt>\c QMAKE_RUN_CC_IMP
<dd>###




<dt>\c QMAKE_RUN_CXX

<dd>###




<dt>\c QMAKE_RUN_CXX_IMP
<dd>###




<dt>\c QMAKE_TARGET

<dd>This variable contains the name of the project target.  The value of
this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c RC_FILE

<dd>This variable contains the name of the resource file for the application.  
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.





<dt>\c RES_FILE

<dd>This variable contains the name of the resource file for the application.  
The value of this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.


<dt>\c SRCMOC
<dd>This variable is set by \e qmake if files can be found that
contain the Q_OBJECT macro.  \c SRCMOC contains the
name of all the generated moc files. The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.


<dt>\c SUBLIBS

<dd>



<dt>\c TARGET_EXT
<dd>This variable specifies the target's extension.  The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.



<dt>\c TARGET_x

<dd>###




<dt>\c TARGET_x.y.z
<dd>###


<dt>\c UICIMPLS
<dd>This variable contains a list of the generated implementation files by UIC.  
The value of this variable
is typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and rarely needs to be
modified.





<dt>\c UICOBJECTS

<dd>This variable is generated from the UICIMPLS variable.  The extension of each 
file will have been replaced by .o (Unix) or .obj (Win32).  The value of this variable is 
typically handled by \e qmake or \link #QMAKESPEC qmake.conf \endlink and
rarely needs to be modified.



<dt>\c VER_MAJ

<dd>This variable contains the major version number of the library, if the
'lib' \link #TEMPLATE template \endlink is specified.





<dt>\c VER_MIN

<dd>This variable contains the minor version number of the library, if the
'lib' \link #TEMPLATE template \endlink is specified.





<dt>\c VER_PAT

<dd>This variable contains the patch version number of the library, if the
'lib' \link #TEMPLATE template \endlink is specified.





<dt>\c YACCIMPLS

<dd>This variable contains a list of yacc source files. The value of
this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.




<dt>\c YACCOBJECTS

<dd>This variable contains a list of yacc object files. The value of
this variable is typically handled by \e qmake or
 \link #QMAKESPEC qmake.conf \endlink and rarely needs to be modified.
</dl>


<a name="DirectoryNote">*</a> For all project files that specify a directory and are interpreted by qmake
(DESTDIR, OBJECTS_DIR, MOC_DIR, etc) the directory will be created when qmake
is called, i.e. before the 'make' command is specified. This is to assure
portablity.

\target Functions
\section1 Functions

For a higher level introduction to using \e qmake functions see
the \link qmake-guide.html#Functions qmake User's
Guide \endlink.

\e qmake recognizes the following functions:

<dl>

<dt>\c include( filename )

<dd>This function will include the contents of \e filename into the
current project at the point it was included.  The function succeeds
if \e filename was included, otherwise it fails. You can check the
return value of this function using a
\link qmake-guide.html#Scopes scope \endlink.

For example:

\code
include( shared.pri )
OPTIONS = standard custom
!include( options.pri ) {
	message( "No custom build options specified" )
	OPTIONS -= custom
}
\endcode

<dt>\c exists( file )

<dd>This function will test if \e file exists, if so it will succeed; otherwise it will
fail. 
You can specify a regular expression in file and it will succeed if any file
matches the regular expression specified.

For example:
\code
exists( $(QTDIR)/lib/qt-mt* ) {
      message( "Configuring for multi-threaded Qt..." )
      CONFIG += thread
}
\endcode

<dt>\c contains( variablename, value )

<dd>This function will succeed if the variable \e variablename
contains the value \e value.  You can check the return value of this
function using a \link qmake-guide.html#Scopes scope \endlink.

For example:

\code
contains( drivers, network ) {
	# drivers contains 'network'
	message( "Configuring for network build..." )
	HEADERS += network.h
	SOURCES += network.cpp
}
\endcode

<dt>\c count( variablename, number )

<dd>This function will succeed if the variable \e variablename
contains \e number elements, otherwise it will fail.  You can check
the return value of this function using a
\link qmake-guide.html#Scopes scope \endlink.

For example:

\code
MYVAR = one two three
count( MYVAR, 3 ) {
	# always true
}
\endcode



<dt>\c isEmpty( variablename )

<dd>This function will succeed if the variable \e variablename is
empty (same as \c count(variable, 0)).

<dt>\c system( command )

<dd>This function will execute \c command in a secondary shell and will
succeed if the command exits with a exit status of 1.You can check the
return value of this function using a
\link qmake-guide.html#Scopes scope \endlink.

For example:

\code
  system(ls /bin):HAS_BIN=FALSE
\endcode

<dt>\c message( string )

<dd>This function will always succeed, and will display the given
\e string to the user.

<dt>\c error( string )

<dd>This function will never return.  It will display the given \e
string to the user, and then exit \e qmake. This function should
only be used for very fatal configurations

For example:

\code
  release:debug:error(You can't have release and debug at the same time!)
\endcode

</dl>

\target Environmemt
\section1 Environment Variables and Configuration

For a higher level introduction to using \e qmake environment
variables and configuration options see the
\link qmake-guide.html#QMAKESPEC qmake User's Guide \endlink.


\target QMAKESPEC
\section2 QMAKESPEC

\e qmake requires a platform and compiler description file which
contains many default values used to generate appropriate makefiles.
The standard Qt distribution comes with many of these files, located
in the 'mkspecs' subdirectory of the Qt installation.

The \link qmake-guide.html#QMAKESPEC QMAKESPEC \endlink environment
variable can contain any of the following:

\list
\i A complete path to a directory containing a \link qmake-guide.html qmake.conf \endlink file.  In this case \e qmake will open the qmake.conf file from within that directory.  If the file does not exist, \e qmake will exit with an error.
\i The name of a platform-compiler combination.  In this case, \e qmake will search in the directory specified by the QTDIR environment variable.
\endlist

Note: the QMAKESPEC path will automatically be added to the
\link #INCLUDEPATHS INCLUDEPATH \endlink system variable.

\target cache
\section2 Cache File

The cache file (mentioned above in the options) is a special file
\e qmake will read to find settings not specified in the
qmake.conf file, the .pro file, or the command line. If neither -path
or -nocache are specified qmake will try to find a file called
.qmake.cache in parent directories.  If it fails to find this file it
will silently ignore this step of processing.

*/
