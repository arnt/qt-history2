; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Qt"
!define PRODUCT_VERSION "%VERSION%"
!define PRODUCT_PUBLISHER "Trolltech"
!define PRODUCT_WEB_SITE "http://www.trolltech.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

var KEY1
var KEY2
var KEY3
var DISPLAY_US_LICENSE
var SET_ENV_VARS

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License check
Page custom CheckQtLicense ValidateKey

; US License page
!define MUI_LICENSEPAGE_RADIOBUTTONS
!define MUI_PAGE_CUSTOMFUNCTION_PRE ShowUSLicense
!insertmacro MUI_PAGE_LICENSE "%PACKAGEDIR%\.LICENSE-US"

; NON-US License page
!define MUI_LICENSEPAGE_RADIOBUTTONS
!define MUI_PAGE_CUSTOMFUNCTION_PRE ShowNonUSLicense
!insertmacro MUI_PAGE_LICENSE "%PACKAGEDIR%\.LICENSE"

; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; Environment setting page
Page custom SetEnvPage SetEnvVariables

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
!define MUI_FINISHPAGE_SHOWREADME
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION "ShowReadMe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
LangString LicenseTitle ${LANG_ENGLISH} "Qt License"
LangString LicenseTitleDescription ${LANG_ENGLISH} "Enter your Qt License key."
LangString EnvTitle ${LANG_ENGLISH} "Set Environment Variables"
LangString EnvTitleDescription ${LANG_ENGLISH} "Set the environment variables for Qt."

ReserveFile "checkqtlicense.ini"
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "%DISTNAME%.exe"
InstallDir "C:\${PRODUCT_NAME}\${PRODUCT_VERSION}"
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File /r "%PACKAGEDIR%\*.*"
  
  ;clean up license files
  strcmp $DISPLAY_US_LICENSE "1" 0 NonUS
    File /oname=".LICENSE" "%PACKAGEDIR%\.LICENSE-US"
    goto End

  NonUS:
    File /oname=".LICENSE" "%PACKAGEDIR%\.LICENSE"

  End:
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\Qt"
  CreateShortCut "$SMPROGRAMS\Qt\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Qt\Uninstall.lnk" "$INSTDIR\uninst.exe"
  CreateShortCut "$SMPROGRAMS\Qt\${PRODUCT_NAME} ${PRODUCT_VERSION} Command Prompt.lnk" "%COMSPEC%" '/k "$INSTDIR\bin\qtvars.bat vsvars"'
SectionEnd

Section -Post
  call PatchQt
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  
  #setting the environment variables
  intcmp $SET_ENV_VARS 0 noenv
    push "$INSTDIR"
    call SetQtEnvVariables
  noenv:
  
  #creating the qtvars.bat file
  push "$INSTDIR"
  call MakeQtVarsFile
  
  #setting the qt version for the integration
  WriteRegStr HKCU "SOFTWARE\trolltech.com\Versions\%VERSION%\" "InstallDir" "$INSTDIR"
SectionEnd

Function .onInit
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "checkqtlicense.ini"
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "setenvpage.ini"
  strcpy $DISPLAY_US_LICENSE "1"
FunctionEnd

Function CheckQtLicense
  push $0
  push $1
  push $2
  qtnsisext::GetLicenseKey
  pop $0
  pop $1
  pop $2
  !insertmacro MUI_INSTALLOPTIONS_WRITE "EnterQtLicensePage.ini" "Field 2" "State" "$0"
  !insertmacro MUI_INSTALLOPTIONS_WRITE "EnterQtLicensePage.ini" "Field 4" "State" "$1"
  !insertmacro MUI_INSTALLOPTIONS_WRITE "EnterQtLicensePage.ini" "Field 6" "State" "$2"

  !insertmacro MUI_HEADER_TEXT "$(LicenseTitle)" "$(LicenseTitleDescription)"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "checkqtlicense.ini"
  pop $2
  pop $1
  pop $0
FunctionEnd

Function SetEnvPage
  !insertmacro MUI_HEADER_TEXT "$(EnvTitle)" "$(EnvTitleDescription)"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "setenvpage.ini"
FunctionEnd

Function PatchQt
  qtnsisext::PatchFile /NOUNLOAD "$INSTDIR\bin\qmake.exe" $INSTDIR
  qtnsisext::PatchFile /NOUNLOAD "$INSTDIR\bin\QtCore400.dll" $INSTDIR
  qtnsisext::PatchFile /NOUNLOAD "$INSTDIR\bin\QtCored400.dll" $INSTDIR
  qtnsisext::PatchFile /NOUNLOAD "$INSTDIR\lib\QtCore400.dll" $INSTDIR
  qtnsisext::PatchFile "$INSTDIR\lib\QtCored400.dll" $INSTDIR
FunctionEnd

Function ValidateKey
  !insertmacro MUI_INSTALLOPTIONS_READ $KEY1 "checkqtlicense.ini" "Field 2" "State"
  !insertmacro MUI_INSTALLOPTIONS_READ $KEY2 "checkqtlicense.ini" "Field 4" "State"
  !insertmacro MUI_INSTALLOPTIONS_READ $KEY3 "checkqtlicense.ini" "Field 6" "State"
  push $1
  IfErrors 0
  qtnsisext::IsValidLicense $KEY1 $KEY2 $KEY3
  IfErrors wrongKey
  pop $1
  strcmp $1 "1" checkForUS
    goto wrongKey

  checkForUS:
    push $1
    qtnsisext::UsesUsLicense $KEY1 $KEY2 $KEY3
    pop $1
    strcmp $1 "1" 0 nonUS
      strcpy $DISPLAY_US_LICENSE "1"
      goto end
  nonUS:
    strcpy $DISPLAY_US_LICENSE "0"
  goto end
  wrongKey:
    MessageBox MB_ICONEXCLAMATION|MB_RETRYCANCEL "The license key you entered is not valid! Do you want to try it again?" IDRETRY tryAgain 0
    Quit
  tryAgain:
    pop $1
    Abort
  end:
    pop $1
FunctionEnd

Function ShowUSLicense
  strcmp $DISPLAY_US_LICENSE "1" +2
  Abort
FunctionEnd

Function ShowNonUSLicense
  strcmp $DISPLAY_US_LICENSE "0" +2
  Abort
FunctionEnd

Function ShowReadMe
  Exec 'notepad.exe "$INSTDIR\README"'
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$SMPROGRAMS\Qt\Uninstall.lnk"
  Delete "$SMPROGRAMS\Qt\Website.lnk"
  Delete "$SMPROGRAMS\Qt\${PRODUCT_NAME} ${PRODUCT_VERSION} Command Prompt.lnk"

  RMDir "$SMPROGRAMS\Qt"
  RMDir /r "$INSTDIR"
  
  #removing the environment variables
  ReadRegDWORD $SET_ENV_VARS ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "QtEnvSet"
  intcmp $SET_ENV_VARS 0 noenv
    push "$INSTDIR"
    call un.RemoveQtEnvVariables
  noenv:
  
  #removing the qt version for the integration
  DeleteRegKey HKCU "SOFTWARE\trolltech.com\Versions\%VERSION%\"
  
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd

Function SetEnvVariables
  push $0
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "setenvpage.ini" "Field 1" "State"
  WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "QtEnvSet" $0
  push $0
  pop $SET_ENV_VARS
  pop $0
FunctionEnd

!include WriteEnvStr.nsh
!include WritePathStr.nsh

#
# to set the qt env. varibles
# push "c:\qt"  #QTDIR
# call setQtEnvVariables
#
Function SetQtEnvVariables
  exch $2 ; the installation path = QTDIR
  push $0 ; I think WriteEnvStr mixes up $0 and $1
  push $1
  
  DetailPrint "Setting QTDIR to $2"
  push "QTDIR"
  push $2
  Call WriteEnvStr ; set the QTDIR

  DetailPrint "Adding $2\bin to PATH"
  push "$2\bin"
  Call AddToPath ; set the PATH
  
  push "QMAKESPEC"
  Call GetMkSpec
  pop $0
  DetailPrint "Setting QMAKESPEC to $0"
  push $0
  Call WriteEnvStr ; set the QMAKESPEC

# we don't need this, right?
;  push "INCLUDE"
;  push $2\include;%INCLUDE%
;  Call WriteEnvStr ; set the PATH

  pop $1
  pop $0
  pop $2
FunctionEnd

#
# removes the qt env. varibles
# push "c:\qt"  #QTDIR
# call un.RemoveQtEnvVariables
#
Function un.RemoveQtEnvVariables
  exch $0 ; QTDIR
  
  DetailPrint "Removing QTDIR"
  push "QTDIR"
  Call un.DeleteEnvStr ; removes QTDIR
  
  DetailPrint "Removing $0\bin from the PATH"
  push "$0\bin"
  Call un.RemoveFromPath ; removes qt from the path
  
  DetailPrint "Removing QMAKESPEC"
  push "QMAKESPEC"
  Call un.DeleteEnvStr ; removes QMAKESPEC

  pop $0
FunctionEnd

#
# returns the makespec
# the result is placed on top of the stack
#
Function GetMkSpec
  push $0

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\8.0" "InstallDir"
  StrCmp $0 "" +1 win32-msvc.net ; found msvc.net 2005

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\7.1" "InstallDir"
  StrCmp $0 "" +1 win32-msvc.net ; found msvc.net 2003

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\7.0" "InstallDir"
  StrCmp $0 "" +1 win32-msvc.net ; found msvc.net 2002

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\6.0\Setup" "VsCommonDir"
  StrCmp $0 "" +1 win32-msvc ; found msvc 6.0

  ReadRegStr $0 HKLM "Software\Intel\Compilers\C++\80" "Major Version"
  StrCmp $0 "" +1 win32-icc ; found icc 8.x

  ; we don't support this compiler, but it doesn't hurt :)
  ReadRegStr $0 HKLM "Software\Intel\Compilers\C++\70" "Major Version"
  StrCmp $0 "" +1 win32-icc ; found icc 7.x

  Goto win32-unknown

  win32-msvc.net:
    pop $0
    push "win32-msvc.net"
    Goto getmkspec_done
  
  win32-msvc:
    pop $0
    push "win32-msvc"
    Goto getmkspec_done
    
  win32-icc:
    pop $0
    push "win32-icc"
    Goto getmkspec_done

  ; unknown compiler
  win32-unknown:
    pop $0
    push "win32-msvc.net" ; fall back on .net

  getmkspec_done:
FunctionEnd

#
# creates a qtvars.bat file in $QTDIR\bin
# push "c:\qt"  #QTDIR
# call MakeQtVarsFile
#
Function MakeQtVarsFile
  exch $1 ; QTDIR
  push $0 ; file handle
  push $2 ; mkspec

  push $1
  call GetMkSpec
  pop $2
  
  ClearErrors
  FileOpen $0 "$1\bin\qtvars.bat" w
  IfErrors done
  FileWrite $0 "@rem$\r$\n"
  FileWrite $0 "@rem This file is generated$\r$\n"
  FileWrite $0 "@rem$\r$\n"
  FileWrite $0 "$\r$\n"
  FileWrite $0 "@echo Setting up a Qt environment...$\r$\n"
  FileWrite $0 "@echo -- QTDIR set to $1$\r$\n"
  FileWrite $0 "@echo -- Added $1\bin to PATH$\r$\n"
  FileWrite $0 "@echo -- QMAKESPEC set to $2$\r$\n"
  FileWrite $0 "$\r$\n"
  FileWrite $0 "@set QTDIR=$1$\r$\n"
  FileWrite $0 "@set PATH=$1\bin;%PATH%$\r$\n"
  FileWrite $0 "@set QMAKESPEC=$2$\r$\n"
  
  call GetVSVarsFile
  pop $2
  strcmp $2 "" novsvars
    FileWrite $0 "$\r$\n"
    FileWrite $0 '@if not "%1"=="vsvars" goto END$\r$\n'
    FileWrite $0 '@call "$2"$\r$\n'
    FileWrite $0 "@:END$\r$\n"
  novsvars:
  FileClose $0
  done:
  pop $2
  pop $0
  pop $1
FunctionEnd

#
# try to get the right vxvarsfile
#
Function GetVSVarsFile
  push $0
  
  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\8.0\Setup\VS" "ProductDir"
  StrCmp $0 "" +1 foundVSDir ; found msvc.net 2005

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\7.1\Setup\VS" "ProductDir"
  StrCmp $0 "" +1 foundVSDir ; found msvc.net 2003

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\7.0\Setup\VS" "ProductDir"
  StrCmp $0 "" +1 foundVSDir ; found msvc.net 2002

  ReadRegStr $0 HKLM "Software\Microsoft\VisualStudio\6.0\Setup\Microsoft Visual Studio" "ProductDir"
  StrCmp $0 "" +1 foundVCDir ; found msvc 6.0
  
  push "" ;empty string if not found
  goto done
  
  foundVSDir:
    push "$0\Common7\Tools\vsvars32.bat"
    goto done
    
  foundVCDir:
    push "$0\vc98\bin\vcvars32.bat"

  done:
    exch
    pop $0
FunctionEnd

