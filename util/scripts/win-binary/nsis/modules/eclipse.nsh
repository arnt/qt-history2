!ifdef MODULE_ECLIPSE

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_INITIALIZE

var ECLIPSE_LOCATION
var ECLIPSE_MINGW_LOCATION

!ifndef MODULE_ECLIPSE_INSTALLER
  !define MODULE_ECLIPSE_INSTALLER "${PRODUCT_NAME} v${PRODUCT_VERSION}"
!endif
!ifndef MODULE_ECLIPSE_ROOT
  !define MODULE_ECLIPSE_ROOT "${INSTALL_ROOT}\eclipse"
!endif
!ifndef MODULE_ECLIPSE_VERSION
  !define MODULE_ECLIPSE_VERSION ${PRODUCT_VERSION}
!endif

!define MODULE_ECLIPSE_QTSTARTUP_ID "com.trolltech.qtstartup_${MODULE_ECLIPSE_VERSION}"
!define MODULE_ECLIPSE_QTPROJECT_ID "com.trolltech.qtproject_${MODULE_ECLIPSE_VERSION}"
!define MODULE_ECLIPSE_QTDESIGNER_ID "com.trolltech.qtdesigner_${MODULE_ECLIPSE_VERSION}"
!define MODULE_ECLIPSE_QTHELP_ID "com.trolltech.help_${MODULE_ECLIPSE_VERSION}"

LangString ModuleEclipsePageTitle ${LANG_ENGLISH} "Eclipse Installation Location"
LangString ModuleEclipsePageDescription ${LANG_ENGLISH} "Select where eclipse is installed, and where MinGW is located."

!define MODULE_ECLIPSE_PAGE "eclipse.ini"
Page custom ModuleEclipsePageEnter ModuleEclipsePageExit

!include "includes\regsvr.nsh"

!macroend ;ECLIPSE_INITIALIZE

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_SECTIONS

Section -PreEclipseSection
  WriteRegStr SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseLocation" $ECLIPSE_LOCATION
SectionEnd

SectionGroup "Eclipse Integration"

Section "Qt Project Integration" ECLIPSE_SEC01
  WriteRegDWORD SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtProjectInstalled" 1
  SetOutPath "$ECLIPSE_LOCATION\plugins\"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\plugins\${MODULE_ECLIPSE_QTSTARTUP_ID}.jar"
  File "${MODULE_ECLIPSE_ROOT}\plugins\${MODULE_ECLIPSE_QTPROJECT_ID}.jar"

  Call InstallQtModules
  
  SetOutPath "$ECLIPSE_INSTDIR"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\bin\qtproparser.dll"
  
  ClearErrors
  push "$ECLIPSE_INSTDIR\qtproparser.dll"
  call RegSvr
  IfErrors 0 +2
    MessageBox MB_OK|MB_ICONEXCLAMATION "Could not register qtproparser.dll"
    
  IfFileExists "$ECLIPSE_MINGW_LOCATION\gcc.exe" 0 done
  Call MakeEclipseStartFile
  CreateShortCut "$SMPROGRAMS\$STARTMENU_STRING\Start Eclipse with MinGW.lnk" "%COMSPEC%" '/c "$ECLIPSE_INSTDIR\start.bat"'

  done:
SectionEnd

Section "Qt Designer Integration" ECLIPSE_SEC02
  WriteRegDWORD SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtDesignerInstalled" 1
  SetOutPath "$ECLIPSE_LOCATION\plugins\"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\plugins\${MODULE_ECLIPSE_QTDESIGNER_ID}.jar"
  
  Call InstallQtModules
  
  SetOutPath "$ECLIPSE_INSTDIR"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\bin\QtDesigner4.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\QtDesignerComponents4.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\qtdesigner.dll"
  
  ClearErrors
  push "$ECLIPSE_INSTDIR\qtdesigner.dll"
  call RegSvr
  IfErrors 0 +2
    MessageBox MB_OK|MB_ICONEXCLAMATION "Could not register qtdesigner.dll"
SectionEnd

Section "Qt Help Integration" ECLIPSE_SEC03
  WriteRegDWORD SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtHelpInstalled" 1
  SetOutPath "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTHELP_ID}"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\plugins\com.trolltech.help\doc.zip"
  File "${MODULE_ECLIPSE_ROOT}\plugins\com.trolltech.help\plugin.xml"
  File "${MODULE_ECLIPSE_ROOT}\plugins\com.trolltech.help\qt.xml"
  SetOutPath "$ECLIPSE_LOCATION\features\${MODULE_ECLIPSE_QTHELP_ID}"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\features\com.trolltech.help\feature.xml"
SectionEnd

SectionGroupEnd

#
# creates a start.bat file with mingw in the path
#
Function MakeEclipseStartFile
  push $0 ; file handle

  ClearErrors
  FileOpen $0 "$ECLIPSE_INSTDIR\start.bat" w
  IfErrors done
  
  FileWrite $0 "@echo off$\r$\n"
  FileWrite $0 "rem$\r$\n"
  FileWrite $0 "rem This file is generated by the installer$\r$\n"
  FileWrite $0 "rem$\r$\n"
  FileWrite $0 "$\r$\n"
  FileWrite $0 "echo Setting up environment...$\r$\n"
  FileWrite $0 "echo -- Using MinGW in: $ECLIPSE_MINGW_LOCATION $\r$\n"
  FileWrite $0 "$\r$\n"
  FileWrite $0 "set PATH=$ECLIPSE_MINGW_LOCATION$\r$\n"
  FileWrite $0 "set PATH=%PATH%;%SystemRoot%\System32$\r$\n"
  FileWrite $0 "$\r$\n"
  FileWrite $0 "echo Starting eclipse...$\r$\n"
  FileWrite $0 "call $ECLIPSE_LOCATION\eclipse.exe"
  FileWrite $0 "$\r$\n"
  FileClose $0
  
  done:
  pop $0
FunctionEnd

Function InstallQtModules
  SetOutPath "$ECLIPSE_INSTDIR"
  SetOverwrite ifnewer
  File "${MODULE_ECLIPSE_ROOT}\bin\msvcp71.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\msvcr71.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\QtCore4.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\QtGui4.dll"
  File "${MODULE_ECLIPSE_ROOT}\bin\QtXml4.dll"
FunctionEnd

Function ModuleEclipsePageEnter
  !insertmacro MUI_HEADER_TEXT "$(ModuleEclipsePageTitle)" "$(ModuleEclipsePageDescription)"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "${MODULE_ECLIPSE_PAGE}"
FunctionEnd

Function ModuleEclipsePageExit
  push $0
  push $1

  !insertmacro MUI_INSTALLOPTIONS_READ $0 "${MODULE_ECLIPSE_PAGE}" "Field 5" "State"
  IfFileExists "$0\eclipse.exe" eclipse_found
  MessageBox MB_OK|MB_ICONSTOP "$0\eclipse.exe not found!$\nPlease select a valid installation directory."
  Goto failed
  eclipse_found:
  
  ClearErrors
  FileOpen $1 "$0\plugins\com.trolltech.writetest" a
  IfErrors 0 has_write_access
  MessageBox MB_OK|MB_ICONSTOP "Can't write to $0\plugins.$\nPlease select a valid installation directory."
  Goto failed
  has_write_access:

  FileClose $1
  Delete "$0\plugins\com.trolltech.writetest"
  StrCpy $ECLIPSE_LOCATION $0
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "${MODULE_ECLIPSE_PAGE}" "Field 3" "State"
  StrCpy $ECLIPSE_MINGW_LOCATION $0
  
  Goto done
  failed:
  pop $1
  pop $0
  Abort
  
  done:
  pop $1
  pop $0
FunctionEnd

!macroend ;ECLIPSE_SECTIONS

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_DESCRIPTION
!ifdef ECLIPSE_SEC01
  !insertmacro MUI_DESCRIPTION_TEXT ${ECLIPSE_SEC01} "This installs the Qt project management system into Eclipse."
!endif
!ifdef ECLIPSE_SEC02
  !insertmacro MUI_DESCRIPTION_TEXT ${ECLIPSE_SEC02} "This installs the Qt Designer Integration."
!endif
!ifdef ECLIPSE_SEC03
  !insertmacro MUI_DESCRIPTION_TEXT ${ECLIPSE_SEC03} "This installs the Qt Reference documentation into Eclipse."
!endif
!macroend

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_STARTUP
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "${MODULE_ECLIPSE_PAGE}"
  SectionSetFlags ${ECLIPSE_SEC01} 17
  SectionSetFlags ${ECLIPSE_SEC02} 1
  SectionSetFlags ${ECLIPSE_SEC03} 1
  
  strcpy $ECLIPSE_INSTDIR "$PROGRAMFILES\Trolltech\Eclipse"
!macroend ;ECLIPSE_STATUP

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_FINISH
!macroend

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_UNSTARTUP
  ReadRegStr $ECLIPSE_LOCATION SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseLocation"
  !insertmacro ConfirmOnRemove "EclipseQtProjectInstalled" "Qt Project Integration"
  !insertmacro ConfirmOnRemove "EclipseQtDesignerInstalled" "Qt Designer Integration"
  !insertmacro ConfirmOnRemove "EclipseQtHelpInstalled" "Qt Help Integration"
!macroend

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_UNINSTALL
Section un."Eclipse Integration"
  push $0

  ReadRegDWORD $0 SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtProjectInstalled"
  intcmp $0 1 0 DoneUnInstallQtProject
    push "$ECLIPSE_INSTDIR\qtproparser.dll"
    call un.RegSvr
  
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTSTARTUP_ID}.jar"
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTPROJECT_ID}.jar"
    Delete "$ECLIPSE_INSTDIR\qtproparser.dll"
    Delete "$SMPROGRAMS\$STARTMENU_STRING\Start Eclipse with MinGW.lnk"
    Delete "$ECLIPSE_INSTDIR\start.bat"
  DoneUnInstallQtProject:

  ReadRegDWORD $0 SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtDesignerInstalled"
  intcmp $0 1 0 DoneUnInstallQtDesigner
    push "$ECLIPSE_INSTDIR\qtdesigner.dll"
    call un.RegSvr
  
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTDESIGNER_ID}.jar"
    Delete "$ECLIPSE_INSTDIR\QtDesigner4.dll"
    Delete "$ECLIPSE_INSTDIR\QtDesignerComponents4.dll"
    Delete "$ECLIPSE_INSTDIR\qtdesigner.dll"
  DoneUnInstallQtDesigner:
  
  ReadRegDWORD $0 SHCTX "$PRODUCT_UNIQUE_KEY" "EclipseQtHelpInstalled"
  intcmp $0 1 0 DoneUnInstallQtHelp
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTHELP_ID}\doc.zip"
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTHELP_ID}\plugin.xml"
    Delete "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTHELP_ID}\qt.xml"
    RMDir "$ECLIPSE_LOCATION\plugins\${MODULE_ECLIPSE_QTHELP_ID}"
    Delete "$ECLIPSE_LOCATION\features\${MODULE_ECLIPSE_QTHELP_ID}\feature.xml"
    RMDir "$ECLIPSE_LOCATION\features\${MODULE_ECLIPSE_QTHELP_ID}"
  DoneUnInstallQtHelp:
  
  Delete "$ECLIPSE_INSTDIR\msvcp71.dll"
  Delete "$ECLIPSE_INSTDIR\msvcr71.dll"
  Delete "$ECLIPSE_INSTDIR\QtCore4.dll"
  Delete "$ECLIPSE_INSTDIR\QtGui4.dll"
  Delete "$ECLIPSE_INSTDIR\QtXml4.dll"
  
  RMDir "$ECLIPSE_INSTDIR"

  pop $0
SectionEnd
!macroend ;ECLIPSE_UNINSTALL

;------------------------------------------------------------------------------------------------
!macro ECLIPSE_UNFINISH
!macroend

!else ;MODULE_ECLIPSE
!macro ECLIPSE_INITIALIZE
!macroend
!macro ECLIPSE_SECTIONS
!macroend
!macro ECLIPSE_DESCRIPTION
!macroend
!macro ECLIPSE_STARTUP
!macroend
!macro ECLIPSE_FINISH
!macroend
!macro ECLIPSE_UNSTARTUP
!macroend
!macro ECLIPSE_UNINSTALL
!macroend
!macro ECLIPSE_UNFINISH
!macroend
!endif ;MODULE_ECLIPSE

