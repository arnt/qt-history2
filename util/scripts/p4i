#!/usr/bin/perl -w

$abort = 0;
$P4 = "p4";

# Simplify program name, if it is a path.
$0 =~ s/.*\///;

open( P4, "$P4 -V |") || die "$0: Couldn't exec $P4: $!\n";
$line = <P4>;
close( P4 );
chomp( $line );
if ( $line !~ /^Perforce/ ) {
    die "$0: $P4 -V isn't p4? Returned: \"$line\"\n";
}

if ( $#ARGV != 0 ) {
    die "$0: takes a single change number as argument\n";
} else {
    $change = $ARGV[0];
}

@fromBranchFiles = ();
@changeDescription = ();
# We don't want the "Affected files ..." section in the description any more,
# so we use this to tell us when we're reading change description, and when
# we're reading the files.
$readingDescription = 1;
# For discarding the empty line after the "Change n by user@client" line.
$eatNextEmptyLine = 0;

# Man, this loop is getting kinda hairy.
open( P4, "$P4 describe -s $change |") || die "$0: Couldn't exec $P4: $!\n";
while( $line = <P4> ) {
    if ( $line =~ /\s*p4i integration$/ ) {
	next;
    }
    if ( $line =~ /^Change [0-9]+ by .* on [0-9]{4}\/[0-9]{2}\/[0-9]{2} [0-9]{2}:[0-9]{2}:[0-9]{2}$/ ) {
	$eatNextEmptyLine = 1;
	next;
    }
    if ( $eatNextEmptyLine && ( $line =~ /^\s*$/ ) ) {
	$eatNextEmptyLine = 0;
	next;
    }
    if ( $line =~ /^Affected files \.\.\./ ) {
	$readingDescription = 0;
    } elsif ( $readingDescription ) {
	push( @changeDescription, $line );
    } elsif ( $line =~ /^\.\.\. (\/\/depot.*)\#([0-9]+)/ ) {
	push( @fromBranchFiles, $1 );
	push( @fromBranchRevisions, $2 );
    }
}
close( P4 );
# The last line is blank, so discard it.
pop( @changeDescription );
foreach $changeLine (@changeDescription) {
    $changeLine =~ s/^[\t ]//;
}

if ( $#fromBranchFiles == -1 ) {
    die "$0: No files found in changelist";
}

# Check all files from same product and in same branch

@pathComponents = split ( /\//, $fromBranchFiles[0] );
# The leading "//" will split into two empty fields, so we use 4 instead of 2
# here. Note that we're making assumptions about the depot layout...

# Different products (e.g. Qt and Qtopia) will have different integration
# targets for different branches...

$targetBranch{"qt-3.0"} = "3.1";
$targetBranch{"qt-3.1"} = "3";
$targetBranch{"qt-3"} = "main";
$targetBranch{"qtopia-1.6"} = "qtopia-1";
$targetBranch{"qtopia-1"} = "main";
$targetBranch{"qtest-main"} = "stable";
$targetBranch{"qtest-stable"} = "main";
$targetBranch{"support-stable"} = "main";
$targetBranch{"support-main"} = "stable";

$product = $pathComponents[3];
$fromBranch = $pathComponents[4];
if ( defined( $targetBranch{"$product-$fromBranch"} ) ) {
    $toBranch = $targetBranch{"$product-$fromBranch"};
} else {
    print STDERR " ** Warning: No target branch found for $product/$fromBranch. Assuming $product/main.\n";
    $toBranch = "main";
}

print "Merging from $fromBranch to $toBranch\n";
if ( $fromBranch eq $toBranch ) {
    die "Can't merge file to itself!\n";
}

foreach $file ( @fromBranchFiles ) {
    @pathComponents = split ( /\//, $file );
    if ( $pathComponents[3] ne $product ) {
	print STDERR "File $file from product $pathComponents[3], not $product\n";
	$abort = 1;
    }
    if ( $pathComponents[4] ne $fromBranch ) {
	print STDERR "File $file on branch $pathComponents[4], not $fromBranch\n";
	$abort = 1;
    }
}

if ( $abort ) {
    die "$0: Exiting; not all files from same product/on same branch";
}

@toBranchFiles = ();
foreach $file ( @fromBranchFiles ) {
    @pathComponents = split ( /\//, $file );
    $pathComponents[4] = $toBranch;
    $toBranchFile = join ("/", @pathComponents );
    push( @toBranchFiles, $toBranchFile );
}

# Check all files are closed
$files = join( " ", map { "\"$_\"" } @toBranchFiles );
print "Checking files are closed: $files\n";
# Unbelievable... p4 writes this file list to stderr!?
$linecount = 0;
open( P4, "$P4 opened $files 2>&1 |") || die "$0: Couldn't exec $P4: $!\n";
while ( $line = <P4> ) {
    $linecount++;
    chomp( $line );
    ($file, $message) = split( m/ - /, $line );
    if ( $message ne "file(s) not opened on this client." ) {
	print STDERR "File $file is open; \"$message\"\n";
	$abort = 1;
    }
}
close( P4 );

if ( !$linecount ) {
    die "$0: No lines read while checking all files closed.\n";
}

if ( $abort ) {
    die "$0: Exiting; not all files were closed.\n";
}

# Check all files are up-to-date newly added

print "Checking files are up-to-date: $files\n";
$linecount = 0;
open( P4, "$P4 sync -n $files 2>&1 |") || die "$0: Couldn't exec $P4: $!\n";
while ( $line = <P4> ) {
    $linecount++;
    chomp( $line );
    ($file, $message) = split( m/ - /, $line );
    if ( $message ne "file(s) up-to-date."
      && $message ne "no such file(s)." ) {
	print STDERR "File $file is not up-to-date; \"$message\"\n";
	$abort = 1;
    }
}
close( P4 );

if ( !$linecount ) {
    die "$0: No lines read while checking all files up-to-date.\n";
}

if ( $abort ) {
    die "$0: Exiting; not all files up-to-date.\n";
}

print "Checking for gaps in integration: $files\n";
for ( $count = 0; $count < @fromBranchFiles; $count++ ) {
    $linecount = 0;
    open( P4, "$P4 integrate -n \"$fromBranchFiles[$count]\@$change\" \"$toBranchFiles[$count]\" |") || die "$0: Couldn't exec $P4: $!\n";
    while ( $line = <P4> ) {
	$linecount++;
	chomp( $line );
	($file, $message) = split( m/ - /, $line );
	if ( $message ne "integrate from $fromBranchFiles[$count]#".$fromBranchRevisions[$count]
	  && $message ne "branch/sync from $fromBranchFiles[$count]#".$fromBranchRevisions[$count]
	  && $message ne "delete from $fromBranchFiles[$count]#".$fromBranchRevisions[$count] ) {
	    print STDERR "Gaps in integration of $file: \"$message\"\n";
	    $message =~ s/.*\#([0-9]+),\#([0-9]+)/$1 $2/g;
	    ($start, $end) = split( " ", $message );
	    printNeededRevisions( $fromBranchFiles[$count], $toBranchFiles[$count], $change, $start, $end);
	    $abort = 1;
	}
    }
    close( P4 );

    if ( !$linecount ) {
	die "$0: No lines read while checking $fromBranchFiles[$count]\@$change for gaps in integration\n";
    }

}

if ( $abort ) {
    die "$0: p4 indicated gaps in integration";
}

# Now do the integrate

print "Integrating: $files\n";
for ( $count = 0; $count < @fromBranchFiles; $count++ ) {
    $linecount = 0;
    open( P4, "$P4 integrate \"$fromBranchFiles[$count]\@$change\" \"$toBranchFiles[$count]\" |") || die "$0: Couldn't exec $P4: $!\n";
    while ( $line = <P4> ) {
	$linecount++;
	chomp( $line );
	# Discard revisions here.
	$line =~ s/\#[0-9]+//g;
	if ( $line ne "$toBranchFiles[$count] - integrate from $fromBranchFiles[$count]"
	  && $line ne "$toBranchFiles[$count] - branch/sync from $fromBranchFiles[$count]"
	  && $line ne "$toBranchFiles[$count] - delete from $fromBranchFiles[$count]" ) {
	    print STDERR "$P4 returned unexpected line \"$line\"\n";
	    $abort = 1;
	}
    }
    close( P4 );

    if ( !$linecount ) {
	die "$0: No lines read while integrating $fromBranchFiles[$count]\@$change\n";
    }

}

if ( $abort ) {
    die "$0: p4 integrate failed on some file(s)";
}

# Now the complicated bit. Doing the resolution.

@filesToResolve = @toBranchFiles;
while ( @filesToResolve ) {
    $files = join( " ", map { "\"$_\"" } @filesToResolve );
    $linecount = 0;
    open( P4, "$P4 resolve -am $files 2>&1 |") || die "$0: Couldn't exec $P4: $!\n";
    while ( $line = <P4> ) {
	$linecount++;
	chomp( $line );
	if ( $line =~ /^\/\// ) {
	    ($file, $message) = split( m/ - /, $line);
	    if ( $message eq "resolve skipped." ) {
		push ( @skippedFiles, $file );
		print "File $file skipped: $message\n";
	    } else {
		print "File $file accepted\n";
	    }
	}
    }
    close( P4 );
    if ( $#skippedFiles > -1 ) {
	print "Skipped these files: ".join( "\n", @skippedFiles )."\n";
	print "Please resolve these files manually, and then press enter.\n";
	$line = <STDIN>;
    }

    if ( !$linecount ) {
	die "$0: No lines read while resolving $files\n";
    }

    @filesToResolve = @skippedFiles;
    @skippedFiles = ();
}


print "Enter y to submit change, anything else to create changelist: ";
$confirm = <STDIN>;

if ( $confirm =~ /^[Yy]/ ) {
    $action = "submit";
} else {
    $action = "change";
}

print "Running 'p4 $action'\n";

open( P4, "| $P4 $action -i" ) || die "$0: Couldn't exec $P4: $!\n";

print P4 "Change: new\n";
print P4 "Description:\n";
print P4 "\tp4i integration\n";
print P4 "\tIntegrate $change from $fromBranch to $toBranch:\n";
print P4 "\t".join( "\t", @changeDescription )."\n";
print P4 "Files:\n\t".join( "\n\t", @toBranchFiles )."\n";

close( P4 );

if ( $action eq "change" ) {
    print "Submit the above change number to complete the integration.\n";
}

print "Done.\n";

sub printNeededRevisions {
    my ($fromBranchFile, $toBranchFile, $change, $start, $end) = @_;
    my ($count, $rev, @fileLog, $revisionChange, $line);

    for ($count = $start; $count <= $end; $count++ ) {
	open( MYP4, "$P4 integrate -n \"$fromBranchFile#$count,#$count\" \"$toBranchFile\" 2>&1 |") || die "$0: Couldn't exec $P4: $!\n";
	$line = <MYP4>;
	chomp( $line );
	close( MYP4 );
	if ( $line ne "$toBranchFile - all revision(s) already integrated." ) {
	    open( MYP4, "$P4 filelog -m 1 \"$fromBranchFile#$count\" |" ) || die "$0: Couldn't exec $P4: $!\n";
	    # The first line is the filename; the second is the rev we want.
	    $line = <MYP4>;
	    $line = <MYP4>;
	    chomp( $line );
	    close( MYP4 );
	    (undef, $rev, undef, $revisionChange, undef) = split ( " ", $line );
	    # We don't report the change we're currently integrating as needing
	    # integration.
	    if ( $revisionChange ne $change ) {
		print "Missing integration of $fromBranchFile revision $rev revisionChange $revisionChange\n";
	    }
	}
    }
}
