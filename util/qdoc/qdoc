#!/usr/bin/perl
#
# $Id: //depot/qt/main/util/qdoc/qdoc#30 $
#
# this is a massive hack, written very quickly
#
# usage: gendoc *.cpp
#        (or just some C++ files)
#
# gendoc looks for comments that start with /*! and look for these
# escapes:
#
# "\e word" emphasises that word
#
# \< -> <
# \> -> >
# \& -> &
# in code examples, \/ becomes //
#
# \arg, \capt, \name, \return and \todo all use the text until the next
# blank line to do, respectively, describe an argument, set a caption
# for the documented unit, set the name which triggers links to the unit,
# describe the return value, and finally add an item to the todo list
#
# "\fn line" uses the rest of the line as a function specification
# (most commonly an inline or virtual = NULL function). It must be the
# first escape
#
# "\class name header" documents the class "name" which is defined in
# header file "<header>".  It too must be the first escape
#
# "\code ... \endcode" makes a code example.  If the first two characters
# on a line in the block are space characters (not white space, ascii 32)
# they will be removed.
#
# "\link name text text text \endlink" makes "text text text" into a link
# "name" which must be a member of this class or fully qualified.
#
# "\page filename.html" directs the rest of the file to that filename.

sub urlencode {
    local($_) = @_;

    s-\!|\%|\/|\<|\=|\>|\[|\]|\^|\||\~|\_-sprintf("_%02x", ord($&))-eg;
    $_;
}

sub html {
    local($_) = @_;

    s/\&/&amp;/gs;
    s/</&lt;/gs;
    s/>/&gt;/gs;
# "
    $_;
}


sub processcode {
    local ($_) = @_;

    $_ = &html($_);
    s/^\s*\n//s;
    s/\s+$/\n/s;
    s/\n\s*\n/\n<br>\n/gs;
    s!\\/(.*?\n)!//$1!gs;
    s/\n  /\n/gs;

    join($_, "<bq><pre>\n", "</pre></bq>\n");
}



sub findlink {
    local ($_, $class, $url, $ismethod) = @_;

    if (defined($anchor{$_}) && $anchor{$_} ne $url && $_ ne $class) {
	sprintf("<a href=%s>%s</a>", $anchor{$_}, $_);
    } elsif ((($ismethod ne "") || (/^[A-Z]/)) && 
	     defined($anchor{$class . $_}) && $anchor{$class . $_} ne $url) {
	sprintf("<a href=%s>%s</a>", $anchor{$class . $_}, $_);
    } elsif (($ismethod ne "") && (defined($inheritance{$class})) &&
	     ($anchor{$class . $_} ne $url)){
	my(@i, $i);
	$i = $inheritance{$class};
	$i =~ s/,\s*\S+\s+(?=\w)/,/s;
	@i = split(/\s*\,\s*/, $i);
	$i = $_;
	while ( ($#i>-1) && ($i eq $_) ) {
	    $i = &findlink( $_, $i[$#i], $url, $ismethod)
		if defined($anchor{$i[$#i]});
	    pop @i;
	}
	sprintf("%s", $i);
    } else {
	sprintf("%s", $_);
    }
}




sub processdocs {
    local ($url, $doc, $class, %word, $idoc);

    foreach $url ( sort keys %type ) {
	$class = $name{$url};
	$class =~ s/::.*/::/;

	$doc = $doc{$url};

	$doc =~ s/\\\&/&amp;/gs;
	$doc =~ s/\\</&lt;/gs;
	$doc =~ s/\\>/&gt;/gs;

	$capt{$url} = $1 
	    if ($doc =~ s/\\capt\s+(.*?)
		(?:(?:\n\s*\n\s*)|(?:$ ))/\n\n/sx);
	$name{$url} = $1
	    if ($doc =~ s/\s*\\name\s+(.*?)
		(?:(?:\n\s*?\n\s*)|(?:$ ))/\n/sx);
	$doc =~ s/\s*$/\n/s;
	$doc =~ s/^\s*//s;
	$doc =~ s-\s*\\warning\s+-<p><strong>Warning: </strong>-gs;
	$doc =~ s/\\internal\s.*//s if (!$internal);

	$doc =~ s-\\link\s+(\S+)\s*(.+?)\s*\\endlink-
	    if (defined($anchor{$1})) {
		sprintf("<a href=%s>%s</a>", $anchor{$1}, $2);
	    } else {
		if (defined($class . "::" . $anchor{$1})) {
		    sprintf("<a href=%s>%s</a>", 
			    $anchor{$class . "::" . $1}, $2);
		} else {
		    print "No link to '", $1, "' for '", $2, "'\n";
		    sprintf("%s", %2);
		}
	    }
	-egsx;

	$doc .= "<p>Returns: " . $1
	    if ($doc =~ s/\s*\\returns?\s+(.*?)
		(?:(?:\n\s*?\n\s*)|(?:$ ))/\n\n/sx);

	if ($doc =~ /\n\\arg\s+/s) {
	    $doc =~ s-\s*\n\\arg\s+-
		\n\nArguments:\n<ul>\n\\arg -sx;
	    $doc =~ s-\n\\arg\s+(.*?)(?=\n\s*\\arg\s+)-\n<li>$1-gs;
	    $doc =~ s-\n\\arg\s+(.*?)
		(?:(?:\n\s*?\n\s*)|(?:$ ))-\n<li>$1\n</ul>\n-sx;
	}

	$_ = "To do:\n<ul>\n";
	$_ .= "<li>" . $1 . "\n" 
	    while ($doc =~ s/\s*\\todo\s+(.*?)
		   (?:(?:\n\s*?\n\s*)|(?:$ ))/\n\n/sx);
	$_ .= "</ul>\n";
	$doc .= $_ if (/<li>/s);

	$doc =~ s-\\internal\s-<p>Internal comments:<p>\n-s;

	$doc =~ s!\\code\s+(.*?)\\endcode!&processcode($1)!esg;

	$doc =~ s!\\e\s+(\S+)!<em>$1</em>!gs;

	$doc =~ s/\s*$/\n/s;
	$doc =~ s-\s*\n\s*\n\s*-\n<p>-gs;

	undef %word;
	$_ = $class;
	s/:://;
	$word{$_}++;

	$doc =~ s-(?:(?:\b((?:(?:operator[^\s\(]{1,3})|(?:\w+)))(\()?)|(<.*?>))-
	    if ($3 eq "") {
		if (!defined($word{$1})) {
		    my ($tmp);
		    $tmp = &findlink($1, $class, $url, $2);
		    $word{$1}++ if ($tmp ne $1);
		    sprintf("%s%s", $tmp, $2);
		} else {
		    sprintf("%s%s", $1, $2);
		}
	    } else {
		sprintf("%s", $3);
	    }
	-egsx;

	$brief{$url} = $1
	    if ($doc =~ s/\s*\\brief\s+(.*?)(?:(?:\n<p>\s*)|(?:$))//s);

	$doc{$url} = $doc;
    }
}


sub mangleclass {
    local($class) = @_;

    $class =~ s/[:\s].*//;
    $class =~ tr/A-Z/a-z/;
    $class = "globals" if ($class eq "");
    $class .= ".html";

    $class;
}




sub manglefn {
    local($type, $class, $name, $sig, $const) = @_;
    local($url, $d);

    $type =~ s/\s+/-/g;
    $type =~ s/^-*(.*?)-*$/$1/;
    $type =~ s/-*([\*\&])-*/$1/g;
    $type = "-" . $type if ($type ne "");
    $const = "-" . $const if ($const ne "");
    $url = join("", &mangleclass($class), "#", $name);
    $d = "(";
    foreach (split(/\s*,\s*/, $sig)) {
# kill argument name
	s/^\s*((?:
		(?:const\s+)|
		(?:volatile\s+)|
		(?:signed\s+)|
		(?:unsigned\s+)|
		(?:long\s+)|
		(?:short\s+)|
		(?:struct\s+)|
		(?:union\s+)|
		(?:class\s+)|
		(?:enum\s+)
		)*\w+[\s\*\&]*?)\s*\b\w+\s*(?:=.*?\s*)?$/$1/gsx;
# strip spaces around & and *
	s/\s*([\*\&])\s*/$1/;
# kill default and change spaces into -
	s/=.*//g;
	s/\s+/-/g;
	s/-+$//;
	s/^-+//;
# qualify enums
	s/\w+/
	    if (defined($anchor{$class . "::" . $&})
		&& $type{$anchor{$class . "::" . $&}} eq "enum") {
		sprintf("%s::%s", $class, $&);
	    } else {
		sprintf("%s", $&);
	    }
	/egsx;

# finally add it to the url
	$url = join($d, $url, $_);
	$d = ",";
    }
    if ($d eq ",") {
	$url .= ")" . $type . $const;
    } else {
	$url .= "()" . $type . $const;
    }

    $url = &urlencode($url);
}




sub storeclass {
    local($filename, $sig, $def) = @_;
    local($url, $name, $inh, $enum);

    ($name, $inh) = split(/\s*:\s*/, $sig);

    $url = &mangleclass($sig);
    $type{$url} = "class";
    $name{$url} = $name;

    $inheritance{$url} = $inh if (defined($inh) && $inh ne "");

    $def =~ s/\s+/ /g;
    $def =~ s/Q_OBJECT//;
    $def =~ s/(\)\s*\b(?:const\s*))\{.*?\}/$1;/gs;
    $def =~ s/;\s*$//s;

    while ($def =~ /(?:\b|^)enum\s+(\w+)\b/gs) {
	$enum = $url . "#" . $1;
	$type{$enum} = "enum";
	$name{$enum} = $name . "::" . $1;
	$anchor{$name{$enum}} = $enum unless (defined($anchor{$name{$enum}}));
    }

    $members{$url} = $def;
}




sub getfile {
    local($filename) = @_;
    local($ifdef);

    open(F, "< $filename") || die "can't open $filename for reading";
    $ifdef = 0;
    while(<F>) {
	$dates{$1} = $2 if (/\$Id\:\s+(.*?),v\s+[0-9\.]+\s+(\S+)\s/);
	$ifdef++ if (/^\#if/ && ($ifdef || !/_H$/)); # medium awful
	if ($ifdef) {
	    print $filename, ":", $., ": ignoring ", $_ if ($debug);
	} else {
	    s-//.*--;
	    push @_,$_;
	}
	$ifdef-- if ($ifdef && /^\#endif\b/); # hm, hm, *_H
    }
    close(F);
    join("", @_);
}



sub readh {
    local ($filename) = @_;

    $h = getfile($filename);
    print "header file: ", $filename, "\n";# if ($debug);
#   $h =~ s/\x22([^\\\x22]|\\.)*\x22//g; # \x22 is "

# { is \x7b and } is \x7d
    while ($h =~ m-\nclass\s+
	   ([^\n]*?)\s+
	   \x7b\s+(.*?)\n\x7d-xsg
	   ) {
	&storeclass($filename, $1, $2);
    }
}

    


sub readc {
    local ($filename) = @_;

    $c = getfile($filename);
    print "code file: ", $filename, "\n";# if ($debug);

    study $c;

# find all class documentation entries
    while ($c =~ m;
	   /\*\![-\s]*	      (?# documentation prologue
			       )
	   \\class\s+         (?# class keyword
			       )
           (\w+)\s+([\.\w]+)  (?# class name and header file
			       )
           (.*?)              (?# actual documentation
			       )
           \*/;gsx) {           # comment end
	my ($class, $hfile, $doc);
	my ($url);
	$class = $1;
	$hfile = $2;
	$doc = $3;
	if (!defined($cfiles{$hfile})) {
	    readh($hfile);
	}
	$filename =~ s-^.*/--;
	$cfiles{$hfile} .= " " . $filename;
	$url = &mangleclass($class);
	print $class, " not in ", $hfile, "\n" 
	    unless (defined($members{$url}));
	$doc{$url} = $doc;
	$includefile{$url} = $hfile;
	$includefile{$url} =~ s/\.doc/.h/;
    }

# find all the defined functions
    while ($c =~ m@
	   (?:/\*\![-\s]*
	    ((?:[^\*]|(?:\*[^/]))*)
	    \*/)\s*
	   ([A-Za-z_](?:[^\{;\*]|(?:\*[^/]))+)\s*\x7b@gsx) {
	my ($doc, $def, $type, $class, $name, $args, $const, $skip);
	$skip = 0;

	$doc = $1;
	$def = $2;
	($name, $args) = split(/\s*\(\s*/, $def);
	if ($name =~ /^\s*(.*?[ \*\&])\s*((?:\w+::)?)(\w+[^(\s\w]{0,3})/s) {
	    $type = $1;
	    $class = $2;
	    $name = $3;
	    $class =~ s/:://;
	    $type =~ s/^\s+//;
	    $type =~ s/\s+$//;
	} else {
	    $skip = 1;
	}
	$args =~ s/([^:]):[^:].*/$1/s;
	if ($args =~ /^(.*?)\s*\)\s*((?:const)?)\s*?$/s) {
	    $args = $1;
	    $const = $2;
	} else {
	    $skip = 1;
	}

	if (!($doc =~ /\\class /) &&
	    ($skip == 0)) {
	    if (!($type =~ /\bstatic\b/)) {
		if ($args =~ /[()]/) {
		    print STDERR "function prototype in argument\n", $def;
		} else {
		    my ($url);
		    $url = &manglefn($type, $class, $name, $args, $const);
		    $name{$url} = $class . "::" . $name;
		    $type{$url} = "fn";
		    $doc{$url} = $doc;
		    $class .= "::" if ($class ne "");
		    $const = " " . const if ($const ne "");
		    $def{$url} = $type." ".$class.$name."(".$args.")".$const;
		    print "url: ", $url, "\n" if ($debug);
		}
	    }
	}
    }

# and function documentation
    while ($c =~ m@
	   /\*\![-\s]*	      (?# documentation prologue
			       )
	   \\fn\s+         (?# fn keyword
			       )
           ((?:[^\(:]*[\s\&\*]+)?)
	   (?:(\w+)::)?
	   ((?:(?:operator[^\(\s]{1,3})|(?:\~?\w+)))
	   (?:\s*\(([^\(\)]*)\)\s*(const)?)?;?\n (?# function signature
						)
           (.*?)              (?# actual documentation
			       )
           \*/@gsx) {           # comment end
	my ($type, $class, $name, $args, $const, $doc);
	my ($url);
	print "url:\n" if ($debug);
	$type = $1;
	$class = $2;
	$name = $3;
	$args = $4;
	$const = $5;
	$doc = $6;
	$all = $&;
	$url = &manglefn($type, $class, $name, $args, $const);
	$name{$url} = $class . "::" . $name;
	$type{$url} = "fn";
	$doc{$url} = $doc;
	$class .= "::" if ($class ne "");
	$def{$url} = $type." ".$class.$name."(".$args.")";
	print "    ", $url, "\n" if ($debug);
     }

# find all extra pages
    while ($c =~ m;
	   /\*\![-\s]*	      (?# documentation prologue
			       )
	   \\page\s+         (?# page keyword
			       )
           (\w+)\s+  (?# file name and optional link trigger
			       )
           (.*?)              (?# file contents
			       )
           \*/;gsx) {           # comment end
	my ($page, $doc);
	my ($url);
	$page = $1;
	$doc = $3;
	$doc = &html($doc);
	$doc =~ s!\\e\s+(\S+)!<em>$1</em>!gs;
    }
}

sub makenames {
    local($_);

    foreach ( sort keys %name ) {
	$anchor{$name{$_}} = $_ unless (defined($anchor{$name{$_}}));
    }
#   foreach ( sort keys %anchor ) { print $_, ": ", $anchor{$_}, "\n"; }
}

#
# add links and escapes to a string
#
sub al {
    local ($_, $class) = @_;

    s-[\w:]+-
	if (defined($anchor{$class . "::" . $&}) &&
	    $type{$anchor{$class."::".$&}} ne "fn") {
	    sprintf("<a href=%s>%s</a>", $anchor{$class."::".$&}, &html($&));
	} elsif (defined($anchor{$&}) &&
		 $type{$anchor{$&}}) {
	    sprintf("<a href=%s>%s</a>", $anchor{$&}, &html($&));
	} else {
	    &html($&);
	}
    -egs;
    $_;
}

#
# add <a href> to args where appropriate and html-encode
#
sub makearglinks {
    local($_, $pre, $name, $post, $class, $anchor);

    foreach (sort keys %type) {
	if ($type{$_} eq "fn" &&
	    $def{$_} =~ /^([^\~\(:]*)\s+
	    ((?:\w+::)?(?:(?:operator[^\(\s]{1,3})|(?:(?:\~?\w+))))\s*
	    (\(.*)/sx) {
	    $pre = $1;
	    $name = $2;
	    $post = $3;
	    $class = $name;
	    $class =~ s/::.*//;
	    if (defined($anchor{$class})) {
		$anchor = $anchor{$class};
		undef $anchor{$class};
		$def{$_} = join(" ", &al($pre, $class), &html($name), 
				&al($post, $class));
		$anchor{$class} = $anchor;
	    } else {
		$def{$_} = join(" ", &al($pre, $class), &html($name), 
				&al($post, $class));
	    }
	}
    }
}




#
# add default values to arguments, from .h argument list
#
# note that argument names aren't touched, just default values
sub fixdefvalues {
    local ($url, $args) = @_;
    local (@ha, @ca, $_, $changed);

    if (defined($def{$url}) && ($args =~ /\,/) && ($def{$url} =~ /\,/)) {
	@ha = split(/\,/, $args);
	$_ = $def{$url};
	s/.*\(//s;
	s/\).*//s;
	@ca = split(/\,/, $_);
	die ("argument mismatch:\n" . $args . "\n" . $_ . "\n")
	    if ($#ha != $#ca);

	$changed = 0;
	foreach ( 0 .. $#ha ) {
	    if ( $ha[$_] =~ /\s*=\s*\S+\s*$/ ) {
		$ca[$_] .= $&;
		$changed++;
	    }
	}
	if ($changed > 0) {
	    $_ = join(",", @ca);
	    $def{$url} =~ s/\(.*?\)/($_)/s;
	}
    }
}



sub printout {
    local($url, $tail, $fnheader, $fntail);

    open(O, "> /dev/null") || die "cant open /dev/null";
    $tail = "";
    $fntail = "";

    foreach $url ( sort keys %type ) {
	my($file);

	$file = $url;
	$file =~ s/#.*//;
	if ($file ne $cfile) {
	    $fnheader = "<h2>Member Function Documentation</h2>\n";
	    print O $fntail, $tail;
	    close(O);
	    open(O, "> $file") || die "can't open $file for writing";
	    print "output html file: ", $file, "\n";
	    $cfile = $file;

	    $tail = "<hr><p>\nThis file is part of the <a href=qt.html>Qt toolkit</a>,\n<a href=licence.html>copyright</a> 1995\n<a href=troll.html>Troll Tech AS</a>, all rights reserved.";
	    if (defined ($cfiles{$includefile{$url}})) {
		my(%files);
		$files{$includefile{$url}}++;
		$tail = join("\n", $tail,
			      "<p>",
			     "It was generated from the following files:",
			     "<ul>",
			     "<li>" . $includefile{$url} .
			     (defined($dates{$includefile{$url}}) ? 
			      (": " . $dates{$includefile{$url}}) :
			      ("")),
			     "");
		foreach (sort split(/\s+/, $cfiles{$includefile{$url}})) {
		    if ($_ ne "" && !defined($files{$_})) {
			$files{$_}++;
			if (defined($dates{$_})) {
			    $tail = join("", $tail, "<li>", $_, ": ", $dates{$_}, "\n");
			} else {
			    $tail = join("", $tail, "<li>", $_);
			}
		    }
		}
		$tail .= "</ul>\n";
	    }
	    $tail .= "<address>\n<a href=mailto:agulbra\@troll.no>Arnt Gulbrandsen</a>, <a href=mailto:all\@troll.no>Troll Tech AS</a>\n</address>\n</body></html>";

	    print O "<html><head>\n<title>Qt toolkit - ", 
	    $name{$url}, 
	    " class</title>\n</head><body>\n",
	    "<h1>", $name{$url}, " Class Reference</h1>\n<hr>\n";
	}
	if ($type{$url} eq "class") {
	    my ($i);
	    print O "<p>\n<code>#include &lt;", $includefile{$url}, "&gt;</code>\n"
		if (defined($includefile{$url}));
	    $i = "";
	    foreach ( sort split(/\s*\,\s*/, $inheritance{$url}) ) {
		my ($im, $ic);
		if (/(\w+)\s+(\w+)/) {
		    $im = $1;
		    $ic = $2;
		} else {
		    $ic = $_;
		    $im = "public";
		}
		if (($ic ne "") && ($im eq "public")) {
		    if (defined($capt{$ic})) {
			$i = join ("", $i, ", ", &al($ic), " (", $capt{$ic}, ")");
		    } else {
			$i = join ("", $i, ", ", &al($ic));
		    }
		}
	    }
	    if ($i ne "") {
		$i =~ s/^\,\s*//;
		$i =~ s/\,([^,]+)$/and$1/;
		print O "<p>\nInherits ", $i, ".\n";
	    }

	    if (defined($members{$url})) {
		my (%m, $mode);
		print O "<p>\n", $brief{$url}, "\n"
		    if (defined($brief{$url}));
		undef %m;
		$mode = "Private";
		foreach (split(/;/, $members{$url})) {
		    $mode = ucfirst($1) if (s/^\s*((?:
						    (?:private\b\s*?)|
						    (?:protected\b\s*?)|
						    (?:public\b\s*?)|
						    (?:signals\b\s*?)|
						    (?:slots\b\s*?))+):\s*//sx);
		    next if (/^\s*friend\b/);
		    if (/((^)|([^\(]*\b))static\b/) {
			s/(\s*)static\s*/$1/;
			$m{"Static ".$mode} .= $_ . ";";
		    } else {
			$m{$mode} .= $_ . ";";
		    }
		}
		foreach $mode ('Public', 'Static Public',
			       'Signals', 'Slots',
			       'Protected', 'Static Protected',
			       'Private', 'Static Private') {
		    my ($e1, $e2);
		    next unless (defined($m{$mode}));
		    $_ = $mode;
		    $_ .= " Members" if (!/s$/);
		    print O "<h2>", ucfirst $_, "</h2>\n<ul>\n";
		    foreach (split (/;/, $m{$mode})) {
			s/\s*([\(\)]+\s*)/ $1 /g;
			s/\s*([,\*\&]+\s*)/$1 /g;
			s-\s*//.*?\n\s*--;
			s/^\s+//;
			s/\s+$//;
			s/\s+/ /g;

			if (/^\s*(?:\}\s*)?((?:[^\(\~]*[\s\&\*]+)?)(?=[\w~]) (?# prelude
							       )
			    ((?:(?:operator[^\(\s]{1,3})|(?:\~?\w+))) (?# name
								   )
			    \s*\(([^\(\)]*)\)\s*(const)?\s*(?:[\{=].*)?$/x) {
			    my ($u,$t,$mt,$f,$a,$ma,$c);
			    $t = $mt = $1;
			    $f = $2;
			    $a = $ma = $3;
			    $c = $4;

			    $u = &manglefn($t, $name{$url}, $f, $a, $c);
			    &fixdefvalues($u, $a) if ($type{$u} eq "fn");
			    next unless ($internal || 
					 ($mode =~ /public/) ||
					 ($doc{$u} ne ""));
			    if ($type{$u} eq "fn") {
				$_ = join("", &html($t), "<a href=", $u,
					  "><strong>", &html($f), 
					  "</strong></a> ( ", &html($a), 
					  " ) ", $c);
			    } else {
				$_ = join("", &html($t), "<strong>", 
					  &html($f), "</strong> ( ", 
					  &html($a), " ) ", $c);
			    }
			    $_ = join ("", $_, " - <em>", $capt{$u}, "</em>")
				if (defined($capt{$u}));
			} else {
			    if (/^\s*enum\s+(\w+)/s &&
				defined($anchor{$name{$url} . "::" . $1})) {
				my($name);
				$name = $1;
				$_ = &html($_);
				s;(\w+[^:\(\{]*)\b(\w+);$1<strong><a name=$name>$2</a></strong>;s;
			    } else {
				$_ = &html($_);
				s;(\w+[^:\(\{]*)\b(\w+);$1<strong>$2</strong>;s;
			    }
			}
			print O "<li>", $_, "\n";
		    }
		    print O "</ul>\n";
		}
		print O "<p><h2>Detailed Description</h2>\n", $doc{$url}, "\n";
	    }
	} elsif (($type{$url} eq "fn") && ($doc{$url} =~ /\S/)) {
	    my ($name);
	    print O $fnheader;
	    $fnheader = "";
	    $fntail = "\n";
	    $_ = $def{$url};
	    $name = $url;
	    $name =~ s/^[^\#]*\#//;
	    s;([^\(]*)\b(\w+);$1<strong><a name=$name>$2</a></strong>;s;
	    print O "<p><h3>", $_, "</h3>\n<p>", $doc{$url};
	}
    }
    print O $fntail, $tail;
    close(O);
    open(O, "> index.html") || die "can't open index.html for writing";
    print O <<EOHEAD
<html>
<head><title>Qt toolkit, main index page</title></head>
<body>
<h1>Qt Index</h1>
<p>Here are the classes that make up Qt:
<ul compact>
EOHEAD
    ;
    foreach $url ( sort keys %type ) {
	print O "<li><a href=", $url, ">", $name{$url}, "</a>\n"
	    if ($type{$url} eq "class");
    }
    print O "</ul>\n<p>Here are the defined enums:\n<ul compact>\n";
    foreach $url ( sort keys %type ) {
	print O "<li><a href=", $url, ">", $name{$url}, "</a>\n"
	    if ($type{$url} eq "enum");
    }
    print O <<EOTAIL
</ul>
<address>
<a href=mailto:agulbra\@troll.no>Arnt Gulbrandsen</a>, <a href=mailto:all\@troll.no>Troll Tech AS</a>
</address>
</body></html>
EOTAIL
    ;
    close(O);
}


$cfile = "VOLD";
#$debug = 1;
$internal = 1;

foreach $arg ( 0..$#ARGV ) {
    &readc($ARGV[$arg]);
}

&makenames();
&makearglinks();
&processdocs();

chdir("/local/qt/doc/html") || die "can't cd to /local/qt/doc/html";

&printout();

