#!/usr/bin/perl -w

use strict;
use Getopt::Long qw(GetOptions);

sub usage {
    print "usage: q2l [-P] [-d] [-y] [-c|-C] [-u] file1.{q2l,book} file2.{q2l,book} ...\n",
	  "-d is debug, i.e. show don't do\n",
	  "-y is produce postscript and don't ask about intermediate steps\n",
	  "-Y as -y but cleanup after\n",
	  "-c is clear intermediate files before and after the run\n",
	  "-C is clear intermediate, log and postscript files\n",
	  "-u is output US letter size (default is A4)\n",
	  "-P is print to PDF and clear\n",
	  ;
    exit;
}

usage() unless @ARGV;

my $DEBUG = 0;
my $YES = 0;
my $SIZE = 'a4';
my $JUSTCLEAN = 0;
my $RUNCLEAN = 0;
my $JUSTDOIT = 0;

Getopt::Long::Configure('no_ignore_case');
GetOptions('help|h' => \&usage,
	   'debug|d' => \$DEBUG,
	   'yes|y' => \$YES,
	   'c' => \$RUNCLEAN,
	   'C' => \$JUSTCLEAN,
	   'u' => \$SIZE,
	   'P' => \$JUSTDOIT,
	  ) or usage();

if ( $JUSTCLEAN or $RUNCLEAN or $JUSTDOIT ) {
    cleanup(1);
    perform("rm -f *.ps");
    perform("rm -f *.pdf");
    exit if $JUSTCLEAN and not @ARGV;
    $RUNCLEAN = 1 if $JUSTCLEAN;
}
my $YESTOPS = $YES;
$YESTOPS = $YES = $JUSTDOIT if $JUSTDOIT;
$SIZE = 'letter' if $SIZE ne 'a4';

my $QTDIR = $ENV{'QTDIR'};
my @tex;
my @idx;
my @dvi;
my @ps;
my @pdf;
my @file;
for my $file ( @ARGV ) {
    $file =~ s/(\.)$/.q2l/;
    $file .= '.q2l' unless $file =~ /\.(q2l|book)$/;
    push @file, $file;
}
@tex = @idx = @dvi = @ps = @pdf = @file;
for ( my $i = 0; $i < scalar @file; $i++ ) {
    $tex[$i] =~ s/\.(q2l|book)$/.tex/;
    $idx[$i] =~ s/\.(q2l|book)$/.idx/;
    $dvi[$i] =~ s/\.(q2l|book)$/.dvi/;
    $ps[$i]  =~ s/\.(q2l|book)$/.ps/;
    $pdf[$i] =~ s/\.(q2l|book)$/.pdf/;
}

perform("$ENV{'QTDIR'}/util/qdoc/qdoc2latex/qdoc2latex -$SIZE $QTDIR/doc/html " . join(" ", @file)) if ask("qdoc2latex");
if ( ask("pslatex (1)") ) {
    for my $tex ( @tex ) {
	perform("pslatex --interaction batchmode $tex >& /dev/null");
    }
}
if ( ask("makeindex") ) {
    for my $idx ( @idx ) {
	perform("makeindex -q $idx");
    }
}
if ( ask("pslatex (2, 3)") ) {
    for my $tex ( @tex ) {
	perform("pslatex --interaction batchmode $tex >& /dev/null");
	perform("pslatex --interaction batchmode $tex >& /dev/null");
    }
}
if ( ask("dvips") ) {
    for my $dvi ( @dvi ) {
	my $ps = shift @ps;
	push @ps, $ps;
	perform("dvips -t $SIZE -q $dvi -o $ps");
    }
}
if ( ask("ps2pdf") ) {
    for my $ps ( @ps ) {
	my $pdf = shift @pdf;
	if ( $SIZE eq 'a4' ) {
	    $pdf =~ s/\.pdf$/-a4.pdf/;
	}
	else {
	    $pdf =~ s/\.pdf$/-us.pdf/;
	}
	perform("ps2pdf $ps $pdf");
    }
}
else {
    if ( ask("pdflatex") ) {
	for my $tex ( @tex ) {
	    perform("pdflatex --interaction batchmode $tex");
	}
    }
}
cleanup($JUSTCLEAN) if $RUNCLEAN;


sub perform {
    if ( $DEBUG ) {
	print shift, "\n";
    }
    else {
	system shift;
    }
}


sub ask {
    return 1 if $YES;
    my $prog = shift;
    return 1 if $YESTOPS and $prog !~ /(?:pdflatex|ps2pdf)/;
    if ( $prog !~ /(?:pdflatex|ps2pdf)/ ) {
	print "\n[Enter] run $prog  [n] no don't run it  [y] yes to postscript  [q] quit? ";
    }
    else {
	print "\n[Enter] run $prog  [n] no don't run it  [q] quit? ";
    }
    my $ans = <STDIN>;
    if ( $ans =~ /^q/i ) {
	cleanup($JUSTCLEAN) if $RUNCLEAN;
	exit;
    }
    $YESTOPS = 1 if $ans =~ /^y/i;
    $ans =~ /^n/i ? 0 : 1;
}


sub cleanup {
    my $all = shift || 0;

    perform("rm -f *.tex");
    perform("rm -f *.dvi");
    perform("rm -f *.ind");
    perform("rm -f *.idx");
    perform("rm -f *.aux");

    if ( $all ) {
	perform("rm -f *.ilg");
	perform("rm -f *.log");
    }

    1;
}
